$Title  WTN_models
$ontext
*--- 废水处理网络最优化设计模型：
*--- 案例数据取自Castro 2007等Ex1-Ex9 Teles 2012Ex10-Ex16 , 16个基本算例
*--- 案例数据取Misener me 2010等 Ex17-Ex20:me4-20, 4个算例
*--- WTN Problem默认采用P formulation，
*--- 基于洞察超结构给定变量初始边界
*--- 默认水处理容量满足最大的需求量，所以不进行限制。
*--- 基于随机多起点NLP局部优化获得高质量可行解
*--- 大规模算例激活多起点局部优化，否则不激活
*--- 算法写完后，记得将普通的wtn和广义问题分开
*--- 不同算例的已知最优解, BL项数
        Ex1 59.80 
        Ex2 130.70 
        Ex3 99.49 
        Ex4 89.84 
        Ex5 229.70 
        Ex6 173.48 
        Ex7 80.78 
        Ex8 109.45 
        Ex9 124.36, 180
        Ex10 586.68 
        Ex11 2127.12 
        Ex12 1201.04 
        Ex13 1564.96 
        Ex14 513.00 
        Ex15 2446.43 
        Ex16 1358.66
        Ex17 me 4,  1086187.1368,   48
        Ex18 me 10, 1086187.1368,   300
        Ex19 me 15, 943734.043623,  675
        Ex20 me 20, 1416366.94,    1260 #注意me20包括两个水阱，有容量限制，其他都是单个水阱
--更新日志：
    7月20日：重新整理代码，基于asm的集合
    7月21日：多起点搜索停止规则简化，给一个最大迭代次数即可
    7月22日：完成了GO算法主体第第二三步，还需要考虑初始化方法交互(没必要），二元变量自适应松弛以及第四步的自适应边界紧缩
    7月23日：添加了Dicopt在gap很小时，切换为模式2，固定初始拓扑结构为松弛问题解的过程；多起点搜索和初始化交互都是可选择的，并不是必须项
            p12约束改进and asm(s_,ma_) and asm(sa_,m_)
            #测试了下二元变量的适应性松弛，效果不好，增加了迭代伦次，和分割变量数，求解时间反而大幅增加，这与原问题中管线解的稀疏性有关，所以没必要松弛
            #还需要考虑的只剩下原问题解初始化松弛问题以及边界紧缩
    7月24日：代码完善，加入求解时间约束，和一次切换dicopt模式，搞定了紧缩框架
    7月25日：初步搞定了边界紧缩的核心部分，以及基于稳健消减策略的执行部分，边界消减策略可以采用激进的策略，未写代码（暂不考虑）
               1区间消减可能性要同时考虑上下界解所在区间无法消减(已搞定)
               2边界消减问题没必要求到最优解！（待搞定）
               3紧缩上下界优先的选择
               4进入Abt的条件(已搞定)
               5离开Abt的条件(已搞定)
            紧缩策略：先紧缩区间数多的，然后中途发现一个变量最远侧区间无法紧缩，则直接跳出紧缩过程（一个折中的紧缩方案）
            报告：每一轮次总的消减区间数
    7月26日：搞定了稳健的Abt执行，改进Abt问题的求解终止条件（不必求到最优）
            Debug:左侧紧缩时，第一个区间是1不是NU
    7月27日： 更新边界紧缩的执行策略
            如果是全部都走一遍，还是有可能出现很多次的无法紧缩，所以在紧缩一步，如果发现一个变量无法紧缩，直接跳出紧缩过程，而不是执行下一个变量。
            改为只紧缩远端的区间，这样效率更高，即使这轮不紧缩近端，下轮就会紧缩另一侧了，减少无效的紧缩执行
            1 执行变量的顺序不变，还是分割区间数多，偏差大
            2 只紧缩远端的子区间，如果无法紧缩则直接结束abt
            3 只对分割区间数大于限定值的变量进行紧缩
           目前看，这个Abt效率是最高的，不再更改Abt策略了，并且发现动态分割效率已经非常搞了，abt其实用处不大。
           完成了Abt过程的代码，进行了debug
    8月14日： 加入Abt过程动态边界更新部分和二元变量推导存在性：
            基于分支流fsm基于两侧浓度边界更新边界和逻辑推导yl
    8月15日： 加入abt紧缩后动态边界更新部分和二元变量推导存在性：
            基于分支流fsm基于两侧浓度边界更新边界和逻辑推导yl

    8月22日：Display的时间和格式调整
$offtext
$eolcom #
*==============================================================================
Option solprint = on ;
Option sysout   = off ;
option MIP      = Gurobi;#Gurobi, Odhcplex, Cplex
option MIQCP    = Dicopt;#Gurobi, Baron, Dicopt
option NLP      = Conopt;#Conopt, Baron
option QCP      = Baron;# Gurobi, Baron, Conopt
option MINLP    = Baron;# Gurobi, Baron, Conopt, Convert, Dicopt
option Optcr    = 0;    # eps_{OPT TOL} = 1 * 10^-3
option Reslim   = 36000;# 1 hour time limit
option Decimals = 3;
option Threads  = 8;
option bratio = 1;#不使用初始点作为求解器的基
*==========================代码块==============================================
$onEcho > WTN_models.gms
*========================Part 1: Abstract model ================================================
*Sets, Parameters, variablbes and equations declared on total Set
*Problem Topology
Sets
    case 'case index'
    i_ 'Effluent source' 
    j_ 'Discharge sinks'         
    l_ 'Treatment units'            
    c_ 'Components'
    s_ 'Splitters'
    m_ 'Mixers'
;
Sets
    i(i_)  'Instance of effluent source'  
    j(j_)  'Instance of discharge sinks'
    l(l_)  'Instance of treatment units' 
    c(c_)  'Instance of Components'
    s(s_)  'Instance of Splitters'
    m(m_)  'Instance of Mixers'
    si(s_) 'Instance of Splitters at the outlet of effluent sources'
    sl(s_) 'Instance of Splitters at the outlet of treatment units'
    mj(m_) 'Instance of Mixers at the inlet of discharge sinks'
    ml(m_) 'Instance of Mixers at the inlet of treatment units'
;
Sets
    iout(i_,s_) 'Allowable connection between i and s'  
    lout(l_,s_) 'Allowable connection between l and s'  
    lin(m_,l_)  'Allowable connection between m and l'
    jin(m_,j_)  'Allowable connection between m and j'
    asm(s_,m_)  'Allowable connection from s to m'
    ssm(s_,m_)  'self-cycle connection from s to m for the same unit'
;
Alias (i,ia),(l,la),(sl,sla),(c,ca);
Alias (s_,sa_),(m_,ma_);
*==============================================================================
Scalar
    Kloopt           'ratio of flowrate of total flow of cyclic of any treatment unit l to the sum of flowrate of total flow of effluent source i'
    Nu               'Unit convert coefficient in generalized pooling problems'  /3600/
    VC_pipe          'Capital Cost coefficient of pipeline (s,m) of the variable part, $/(m*t/h*y)' /3603.4/ # me
    FC_pipe          'Capital Cost coefficient of pipeline (s,m) of the fixed part, $/(m*t/h*y)'  /124.6/#0.2-5 me
    FpipeUp          'addtitional limitation of maximum allowable water flowrate of the pipe, t/h' /1E+4/#a big enough number
    FpipeLo          'addtitional limitation of minimum allowable water flowrate of the pipe, t/h' /0.2/#0.2me,1,5Baga 

Parameters
    Fi(i_)           'Flowrate of total flow of effluent source i, t/h'      
    Pic(i_,c_)       'Concentration of component c of effluent source i, 10^3 ppm'
    Rlc(l_,c_)       'Removal ratio of component c of treatment unit l'
    PlcinU(l_,c_)    'Upper bound of concentration of component c at the inlet of treatment unit l, 10^3 ppm'
    PjcU(j_,c_)      'Upper bound of concentration of component c at the inlet of discharge sink j, 10^3 ppm'
    OCl(l_)          'investment cost coefficient for treatment unit l'
    ICl(l_)          'operating cost coefficient for treatment unit l'
    D_pipe(s_,m_)    'length of the pipeline (s,m), m'
    FjU(j_)          'Flowrate upper bound of total flow of effluent sink j, t/h'
    Ymeyer20         'is the case meyer20'
    NlU_meyer20      'upper bound of active treatment in meyer20'
;
Parameters #Auxiliary Paratermeter declaration
    Yunitselfrecycled  'is self-cylce allowable for process branch stream'
    Ygpp               'the problem belongs to generalized pooling problem——gpp'
    Fsm_USol(s_,m_)    'Flowrate of total flow from splitter s to mixer m, t/h'
    Fl_USol(l_)        'Flowrate of total flow of treatment unit l, t/h'
    Plcin_USol(l_,c_)  'Concentration of component c at the inlet of l, ppm'
    Plcout_USol(l_,c_) 'Concentration of component c at the outlet of l, ppm'                                                
    Pjc_USol(j_,c_)    'Concentration of component c of discharge sink j, ppm'
    Fil_USol(i_,l_)    'Flowrate of total flow from i to l, t/h'
    Fij_USol(i_,j_)    'Flowrate of total flow from i to j, t/h'
    Fll_USol(l_,l_)    'Flowrate of total flow from l to ll, t/h'
    Flj_USol(l_,j_)    'Flowrate of total flow from l to j, t/h'
    Report(*,*)           'Report'
    Report_mul(*,*)
;


*=======================================================================
Positive Variables
    fsm(s_,m_)      'Flowrate of total flow from splitter s to mixer m, t/h'
    plcout(l_,c_)   'Concentration of component c at the outlet of treatment unit l, 10^3 ppm'
    fl(l_)          'Flowrate of total flow of treatment l, t/h'
    #Variables used only for RP model
    fsmc(s_,m_,c_)   'Flowrate of component flow c from splitter s to mixer m, kg/h'
;
Binary variables
    yl(l_) #Ygpp
    ysm(s_,m_)#Ygpp
;
Variable  objval;
*=======================================================================
Equations
    p0_objfun       'objective function'
    p0_objfun_gpp   'objective function gpp'
    p1(i_,s_)       'mass balance of total flow at si'  
    p2(m_,l_,s_)    'mass balance of total flow at l' 
    p3(l_,s_)       'calculation of fl(l)'
    p4(m_,l_,s_,c_) 'mass balance of component flow c  at l'
    p4_RP(m_,l_,s_,c_) 'mass balance of component flow c  at l'
    p5(m_,l_,c_)    'Concentration upper bound of component  c  at lin'
    p5_RP(m_,l_,c_)    'Concentration upper bound of component  c  at lin'
    p6(m_,j_,c_)    'Concentration upper bound of component  c  at mjin'
    p6_RP(m_,j_,c_)    'Concentration upper bound of component  c  at mjin'
    p7(l_)          'Pooling Capacity constraints'                              
    p8(s_,m_)       'Pipeline flowrate upper bound constraints'
    p9(s_,m_)       'Pipeline flowrate lower bound constraints'
    p10(m_,j_)      'Sink flowrate constraints'
    p11             'Overal flowrate balance'
    p12             'Looping between two treatment units is disallowed'
    p13             'Meyer20 activate treatments upper bound'
;

#======================== Models (declared on total Set and defined on subSet) ================================================
    #objective function: sum of total flowrate of treatment units 
    p0_objfun..                                 objval =e= sum(l, fl(l));
    p0_objfun_gpp..                             objval =e= sum(l, OCl(l)*fl(l)+ ICl(l)*yl(l))
                                                           + sum(asm(s,m), (VC_pipe*fsm(s,m)/Nu
                                                                           + FC_pipe*ysm(s,m))*D_pipe(s,m))          
                                                           ;
    
    #mass balance of total flow at si 
    p1(i, si)$iout(i, si)..                     Fi(i) =e= sum(m$asm(si,m), fsm(si,m));
    
    #mass balance of total flow at l 
    p2(ml,l,sl)$(lin(ml,l) and lout(l,sl)) ..   sum(s$asm(s,ml), fsm(s,ml)) =e= sum(m$asm(sl,m), fsm(sl,m));
    
    #calculation of fl(l)
    p3(l,sl)$(lout(l,sl))..                     fl(l) =e= sum(m$asm(sl,m), fsm(sl,m));
    
    #mass balance of component flow c  at l
    p4(ml,l,sl,c)$(lout(l,sl) and lin(ml,l))..
                                                sum(m$asm(sl,m), fsm(sl,m))*plcout(l,c) =e= (1- Rlc(l,c))*
                                                (sum((si,i)$(iout(i,si) and asm(si,ml)), fsm(si,ml)*Pic(i,c))
                                                + sum((la,sla)$(lout(la,sla) and asm(sla,ml)), fsm(sla,ml)*plcout(la,c)))
                                                ;
                                                
    p4_RP(ml,l,sl,c)$(lout(l,sl) and lin(ml,l))..
                                                sum(m$asm(sl,m), fsmc(sl,m,c)) =e= (1- Rlc(l,c))*
                                                (sum((si,i)$(iout(i,si) and asm(si,ml)), fsm(si,ml)*Pic(i,c))
                                                + sum((la,sla)$(lout(la,sla) and asm(sla,ml)), fsmc(sla,ml,c)))
                                                ;
    
    #Concentration upper bound of component  c  at lin
    p5(ml,l,c)$(lin(ml,l) and 1-Ygpp)..         sum((i,si)$(iout(i,si) and asm(si,ml)), fsm(si,ml)*Pic(i,c))
                                                + sum((la,sl)$(lout(la,sl) and asm(sl,ml)), fsm(sl,ml)*plcout(la,c))
                                                =l=
                                                PlcinU(l,c)*sum(s$asm(s,ml), fsm(s,ml))
                                                ;
    p5_RP(ml,l,c)$(lin(ml,l) and 1-Ygpp)..      sum((i,si)$(iout(i,si) and asm(si,ml)), fsm(si,ml)*Pic(i,c))
                                                + sum((la,sl)$(lout(la,sl) and asm(sl,ml)), fsmc(sl,ml,c))
                                                =l=
                                                PlcinU(l,c)*sum(s$asm(s,ml), fsm(s,ml))
                                                ;
                                                
    #Concentration upper bound of component  c  at mjin
    p6(mj,j,c)$jin(mj,j)..                      sum((i,si)$(iout(i,si) and asm(si,mj)), fsm(si,mj)*Pic(i,c))
                                                + sum((l,sl)$(lout(l,sl) and asm(sl,mj)), fsm(sl,mj)*plcout(l,c))
                                                =l=
                                                PjcU(j,c)*sum(s$asm(s,mj), fsm(s,mj));
    
    p6_RP(mj,j,c)$jin(mj,j)..                   sum((i,si)$(iout(i,si) and asm(si,mj)), fsm(si,mj)*Pic(i,c))
                                                + sum((l,sl)$(lout(l,sl) and asm(sl,mj)), fsmc(sl,mj,c))
                                                =l=
                                                PjcU(j,c)*sum(s$asm(s,mj), fsm(s,mj));
                                                
    #-------Extensions:Generalized pooling problems--
    #Pooling Capacity constraint:
    p7(l)..                                     fl(l) =l= fl.Up(l)*yl(l);
    #Pipeline flowrate constraints:                                           
    p8(s,m)$asm(s,m) ..                         fsm(s,m) =l= min(FpipeUp, fsm.Up(s,m))*ysm(s,m);
    p9(s,m)$asm(s,m)..                          fsm(s,m) =g= max(FpipeLo, fsm.Lo(s,m))*ysm(s,m);
    
    #Sink flowrate constraints:                                           
    p10(mj,j)$jin(mj,j)..                       sum(s$asm(s,mj), fsm(s,mj)) =l= min(sum(i, Fi(i)),FjU(j));
    
    #Overall flowrate balance:                                           
    p11..                                       sum(asm(s,mj), fsm(s,mj)) =e= sum(i, Fi(i));
    
    #Looping between two treatment units is disallowed:                                          
    p12(s_,m_,sa_,ma_)$(ord(s_)<ord(sa_) and ord(s_)=ord(m_) and ord(sa_)=ord(ma_)
                        and sl(s_) and ml(m_) and sl(sa_) and ml(ma_) and asm(s_,ma_) and asm(sa_,m_))..
                                                ysm(s_,ma_) + ysm(sa_,m_) =l= 1;
                                                
    p13$Ymeyer20..                              sum(l, yl(l)) =l= NlU_meyer20;
                                                
    
Equations
    fsmc_MC1(l_,s_,m_,c_)
    fsmc_MC2(l_,s_,m_,c_)
    fsmc_MC3(l_,s_,m_,c_)
    fsmc_MC4(l_,s_,m_,c_)
;
    #========================Mc envelope for relaxed variables =========
    #fsmc(s_,m_,c_)   'Flowrate of component flow c from splitter sl to mixer m, kg/h'
    fsmc_MC1(l,sl,m,c)$(lout(l,sl) and asm(sl,m))..          fsmc(sl,m,c) =g= plcout.Up(l,c)*fsm(sl,m) + plcout(l,c)*fsm.Up(sl,m) -
                                                                              plcout.Up(l,c)*fsm.Up(sl,m);
    fsmc_MC2(l,sl,m,c)$(lout(l,sl) and asm(sl,m))..          fsmc(sl,m,c) =g= plcout.Lo(l,c)*fsm(sl,m) + plcout(l,c)*fsm.Lo(sl,m) -
                                                                              plcout.Lo(l,c)*fsm.Lo(sl,m);
    fsmc_MC3(l,sl,m,c)$(lout(l,sl) and asm(sl,m))..          fsmc(sl,m,c) =l= plcout.Up(l,c)*fsm(sl,m) + plcout(l,c)*fsm.Lo(sl,m) -
                                                                              plcout.Up(l,c)*fsm.Lo(sl,m);
    fsmc_MC4(l,sl,m,c)$(lout(l,sl) and asm(sl,m))..          fsmc(sl,m,c) =l= plcout.Lo(l,c)*fsm(sl,m) + plcout(l,c)*fsm.Up(sl,m) -
                                                                              plcout.Lo(l,c)*fsm.Up(sl,m);
#====================================PMR Model(nf4r_formula)=====================================
Set npmr 'PMR 分割区间数总集合' /1*1000/;
Set l_c_npmr(l_,c_,npmr) '不同plcout对应的分割区间数';
Parameters PlcoutnUp_pmr(l_,c_,npmr) '子区间浓度上界', PlcoutnLo_pmr(l_,c_,npmr) '子区间浓度下界';
Positive variables fsmcn_pmr_d(s_,m_,c_,npmr) '凸包重构后fsm(sl,m)对应于plcout(l,c,npmr)的分解变量, 一定要引入下标c, 因为不同杂质对应的npmr是也就是(c,npmr)是独立，激活的npmr是不相关的';
Binary Variables yplcout_pmr(l_,c_,npmr) 'plcout是否落在浓度子区间 npmr 的二元变量';
Equations
    eqbiplcout_pmr1(l_,c_)                   
    eqplcoutU_pmr2a(l_,c_)                     
    eqplcoutL_pmr2b(l_,c_)                      
    eqsumt_pmr3a(l_,s_,m_,c_)                         
    eqfsmcnU_pmr3b(l_,s_,m_,c_,npmr)                  
    eqfsmcnL_pmr3c(l_,s_,m_,c_,npmr)                     
    fsmc_pmr4a(l_,s_,m_,c_)                             
    fsmc_pmr4b(l_,s_,m_,c_)                         
    fsmc_pmr4c(l_,s_,m_,c_)                                 
    fsmc_pmr4d(l_,s_,m_,c_)
;

    eqbiplcout_pmr1(l,c)..                          sum(npmr$l_c_npmr(l,c,npmr), yplcout_pmr(l,c,npmr)) =e= 1;
    eqplcoutU_pmr2a(l,c)..                          plcout(l,c) =l= sum(npmr$l_c_npmr(l,c,npmr), PlcoutnUp_pmr(l,c,npmr)*yplcout_pmr(l,c,npmr));
    eqplcoutL_pmr2b(l,c)..                          plcout(l,c) =g= sum(npmr$l_c_npmr(l,c,npmr), PlcoutnLo_pmr(l,c,npmr)*yplcout_pmr(l,c,npmr));  
    eqsumt_pmr3a(l,sl,m,c)$(lout(l,sl) and asm(sl,m))..
                                                    sum(npmr$l_c_npmr(l,c,npmr), fsmcn_pmr_d(sl,m,c,npmr)) =e= fsm(sl,m);        
    eqfsmcnU_pmr3b(l,sl,m,c,npmr)$(lout(l,sl) and asm(sl,m) and l_c_npmr(l,c,npmr))..
                                                    fsmcn_pmr_d(sl,m,c,npmr) =l= fsm.Up(sl,m)*yplcout_pmr(l,c,npmr);
    eqfsmcnL_pmr3c(l,sl,m,c,npmr)$(lout(l,sl) and asm(sl,m) and l_c_npmr(l,c,npmr))..
                                                    fsmcn_pmr_d(sl,m,c,npmr) =g= fsm.Lo(sl,m)*yplcout_pmr(l,c,npmr);
    fsmc_pmr4a(l,sl,m,c)$(lout(l,sl) and asm(sl,m))..
                                                    fsmc(sl,m,c) =g= sum(npmr$l_c_npmr(l,c,npmr), plcoutnUp_pmr(l,c,npmr)*fsmcn_pmr_d(sl,m,c,npmr)) + fsm.Up(sl,m)*plcout(l,c) - sum(npmr$l_c_npmr(l,c,npmr), plcoutnUp_pmr(l,c,npmr)*fsm.Up(sl,m)*yplcout_pmr(l,c,npmr));                                 
    fsmc_pmr4b(l,sl,m,c)$(lout(l,sl) and asm(sl,m))..
                                                    fsmc(sl,m,c) =g= sum(npmr$l_c_npmr(l,c,npmr), plcoutnLo_pmr(l,c,npmr)*fsmcn_pmr_d(sl,m,c,npmr)) + fsm.Lo(sl,m)*plcout(l,c) - sum(npmr$l_c_npmr(l,c,npmr), plcoutnLo_pmr(l,c,npmr)*fsm.Lo(sl,m)*yplcout_pmr(l,c,npmr));                                 
    fsmc_pmr4c(l,sl,m,c)$(lout(l,sl) and asm(sl,m))..
                                                    fsmc(sl,m,c) =l= sum(npmr$l_c_npmr(l,c,npmr), plcoutnUp_pmr(l,c,npmr)*fsmcn_pmr_d(sl,m,c,npmr)) + fsm.Lo(sl,m)*plcout(l,c) - sum(npmr$l_c_npmr(l,c,npmr), plcoutnUp_pmr(l,c,npmr)*fsm.Lo(sl,m)*yplcout_pmr(l,c,npmr));                                 
    fsmc_pmr4d(l,sl,m,c)$(lout(l,sl) and asm(sl,m))..
                                                    fsmc(sl,m,c) =l= sum(npmr$l_c_npmr(l,c,npmr), plcoutnLo_pmr(l,c,npmr)*fsmcn_pmr_d(sl,m,c,npmr)) + fsm.Up(sl,m)*plcout(l,c) - sum(npmr$l_c_npmr(l,c,npmr), plcoutnLo_pmr(l,c,npmr)*fsm.Up(sl,m)*yplcout_pmr(l,c,npmr));                                                                                                                                         
$offecho
$include WTN_models


Model WTN/
    p0_objfun     
    p1       
    p2
    p3
    p4
    p5
    p6
    /;
Model WTN_gpp/
    p0_objfun_gpp     
    p1       
    p2
    p3
    p4
    p5
    p6
    p7
    p8
    p9
    p10
    p11
    p12
    p13
    /;
Model WTN_gpp_RP_P1/
    p0_objfun_gpp     
    p1       
    p2
    p3
    p4_RP
    p5_RP
    p6_RP
    p7
    p8
    p9
    p10
    p11
    p12
    p13
    fsmc_MC1
    fsmc_MC2
    fsmc_MC3
    fsmc_MC4
    /;
Model WTN_gpp_RP_PN/
    p0_objfun_gpp     
    p1       
    p2
    p3
    p4_RP
    p5_RP
    p6_RP
    p7
    p8
    p9
    p10
    p11
    p12
    p13
    eqbiplcout_pmr1                  
    eqplcoutU_pmr2a                    
    eqplcoutL_pmr2b                     
    eqsumt_pmr3a                       
    eqfsmcnU_pmr3b                  
    eqfsmcnL_pmr3c                    
    fsmc_pmr4a                             
    fsmc_pmr4b                         
    fsmc_pmr4c                                
    fsmc_pmr4d
    /;
Model WTN_gpp_RP_PN_Abt/
    p0_objfun_gpp     
    p1       
    p2
    p3
    p4_RP
    p5_RP
    p6_RP
    p7
    p8
    p9
    p10
    p11
    p12
    p13
    eqbiplcout_pmr1                  
    eqplcoutU_pmr2a                    
    eqplcoutL_pmr2b                     
    eqsumt_pmr3a                       
    eqfsmcnU_pmr3b                  
    eqfsmcnL_pmr3c                    
    fsmc_pmr4a                             
    fsmc_pmr4b                         
    fsmc_pmr4c                                
    fsmc_pmr4d
    /;
$onecho > Data_input.gms
* ==================================Part 2: Data import======================
* This part includes Set and data definition,also include bounds of Variables definition
Sets
    i_     /1*20/
    j_     /1*20/
    l_     /1*20/
    c_     /1*20/
    s_     /1*40/
    m_     /1*40/
    case   'case index'/Ex1*Ex16, me4, me10, me15, me20/;

*Problem Parameters of all cases 
Table Fi_all(case,i_)
          1     2    3     4    5    6    7
    Ex1   40    30   20
    Ex2   60    20
    Ex3   20    30   50
    Ex4   40    40
    Ex5   13.1  32.7 56.5
    Ex6   13.1  32.7 56.5
    Ex7   20    15   5
    Ex8   19    7    8     6    17
    Ex9   19    7    8     6    17
    Ex10  80    60   130
    Ex11  80    450  230   90   330
    Ex12  90    350  200   40   130
    Ex13  90    50   200   240  530  70
    Ex14  50    120  70
    Ex15  150   70   900
    Ex16  150   270
    me4   20    50   47.5  28   100  30   25
    me10  20    50   47.5  28   100  30   25
    me15  20    50   47.5  28   100  30   25
    me20  20    50   47.5  28   100  30   25
#         1     2    3     4    5    6    7
; 
Table Pic_all(case,i_,c_) 
            1     2      3      4      5      6   
    Ex1.1   400
    Ex1.2   100
    Ex1.3   30
    Ex2.1   400
    Ex2.2   800
    Ex3.1   800
    Ex3.2   400
    Ex3.3   200
    Ex4.1   100   20    
    Ex4.2   15    200
    Ex5.1   10    390    25
    Ex5.2   110   16780  40
    Ex5.3   100   25     35    
    Ex6.1   10    390    250
    Ex6.2   110   16780  400
    Ex6.3   100   25     350
    Ex7.1   600   500    500 
    Ex7.2   400   200    100 
    Ex7.3   200   1000   200 
    Ex8.1   1100  500    500    200    800    100
    Ex8.2   40    0      100    300    910    200
    Ex8.3   200   220    200    500    150    0
    Ex8.4   60    510    500    200    780    100 
    Ex8.5   400   170    100    300    900    0
    Ex9.1   1100  500    500    200    800    100
    Ex9.2   40    0      100    300    910    200
    Ex9.3   200   220    200    500    150    0
    Ex9.4   60    510    500    200    780    100 
    Ex9.5   400   170    100    300    900    0
    Ex10.1  12  
    Ex10.2  50    
    Ex10.3  500
    Ex11.1  12 
    Ex11.2  50    
    Ex11.3  500
    Ex11.4  400  
    Ex11.5  120
    Ex12.1  330 
    Ex12.2  50    
    Ex12.3  150
    Ex12.4  240  
    Ex12.5  120
    Ex13.1  12 
    Ex13.2  350    
    Ex13.3  500
    Ex13.4  400  
    Ex13.5  50
    Ex13.6  140
    Ex14.1  10    200
    Ex14.2  110   140  
    Ex14.3  100   25
    Ex15.1  10    200    350
    Ex15.2  110   140    100
    Ex15.3  100   25     90
    Ex16.1  210   700
    Ex16.2  110   140
    me4.1   100   500   500
    me4.2   800   1750  2000
    me4.3   400   80    100
    me4.4   1200  1000  400
    me4.5   500   700   250
    me4.6   50    100   50
    me4.7   1000  50    150
    me10.1  100   500   500
    me10.2  800   1750  2000
    me10.3  400   80    100
    me10.4  1200  1000  400
    me10.5  500   700   250
    me10.6  50    100   50
    me10.7  1000  50    150
    me15.1  100   500   500
    me15.2  800   1750  2000
    me15.3  400   80    100
    me15.4  1200  1000  400
    me15.5  500   700   250
    me15.6  50    100   50
    me15.7  1000  50    150
    me20.1  100   500   500
    me20.2  800   1750  2000
    me20.3  400   80    100
    me20.4  1200  1000  400
    me20.5  500   700   250
    me20.6  50    100   50
    me20.7  1000  50    150;
;
#           1     2      3      4      5      6   

Table PjcU_all(case,j_,c_)
            1    2    3      4      5      6    
    Ex1.1   20
    Ex2.1   10
    Ex3.1   30
    Ex4.1   10   10
    Ex5.1   2    2    5
    Ex6.1   20   5    100
    Ex7.1   100  100  100 
    Ex8.1   100  100  100    100    100    100
    Ex9.1   100  100  100    100    100    100
    Ex10.1  2
    Ex11.1  4
    Ex12.1  10
    Ex13.1  4
    Ex14.1  10   5
    Ex15.1  10   15   20
    Ex16.1  5    15
    me4.1   5    5    10
    me10.1  5    5    10
    me15.1  5    5    10
    me20.1  5    5    10
    me20.2  5    5    10
;

Table Rlc_all(case,l_,c_)
            1      2      3      4      5      6
    Ex1.1   0.99
    Ex2.1   0.99
    Ex2.2   0.80
    Ex3.1   0.90
    Ex3.2   0.99
    Ex4.1   0.95   0
    Ex4.2   0      0.976     
    Ex5.1   0      0.999  0
    Ex5.2   0.90   0.90   0.97
    Ex5.3   0.95   0      0.20
    Ex6.1   0      0.999  0
    Ex6.2   0.70   0.90   0.98
    Ex6.3   0.70   0      0.50
    Ex7.1   0.90     
    Ex7.2   0      0.99   0
    Ex7.3   0      0      0.80  
    Ex8.1   0.99   0.99  
    Ex8.2   0      0      0.99   0.99
    Ex8.3   0      0      0      0      0.99   0.99
    Ex9.1   0.99   0  
    Ex9.2   0      0.99     
    Ex9.3   0      0      0.99     
    Ex9.4   0      0      0      0.99   0.90
    Ex9.5   0      0      0      0      0.99   0.99
    Ex10.1  0.10   
    Ex10.2  0.40         
    Ex10.3  0.85    
    Ex10.4  0.74     
    Ex10.5  0.90
    Ex11.1  0.10   
    Ex11.2  0.40         
    Ex11.3  0.85    
    Ex11.4  0.74     
    Ex11.5  0.90
    Ex11.6  0.60     
    Ex11.7  0.70
    Ex12.1  0.95   
    Ex12.2  0.80         
    Ex12.3  0.85    
    Ex12.4  0.12     
    Ex12.5  0.30
    Ex12.6  0.60     
    Ex12.7  0.67
    Ex12.8  0.70
    Ex12.9  0.60     
    Ex12.10 0.70
    Ex13.1  0.95   
    Ex13.2  0.20         
    Ex13.3  0.85    
    Ex13.4  0.74     
    Ex13.5  0.10
    Ex13.6  0.60     
    Ex13.7  0.67
    Ex13.8  0.70
    Ex13.9  0.50     
    Ex13.10 0.50
    Ex13.11 0.30   
    Ex13.12 0.88         
    Ex13.13 0.85    
    Ex13.14 0.74     
    Ex13.15 0.45
    Ex14.1  0.90   0  
    Ex14.2  0.70   0.90        
    Ex14.3  0      0.80
    Ex14.4  0.50   0 
    Ex14.5  0.65   0.60
    Ex15.1  0.90   0      0.70
    Ex15.2  0.70   0.90   0.40      
    Ex15.3  0      0.80   0
    Ex16.1  0.90   0 
    Ex16.2  0.70   0.90      
    Ex16.3  0      0.80
    Ex16.4  0.50   0  
    Ex16.5  0.90   0.70
    Ex16.6  0.20   0.40
#           1      2      3      4      5      6
    me4.1   0.99   0.90   0.95
    me4.2   0.00   0.87   0.90
    me4.3   0.10   0.99   0.00
    me4.4   0.7    0.2    0.3
    me10.1  0.90   0.95   0.00
    me10.2  0.875  0.500  0.500
    me10.3  0.99   0.90   0.95
    me10.4  0.00   0.75   0.75
    me10.5  0.9    0.9    0.2
    me10.6  0.00   0.00   0.95
    me10.7  0.00   0.87   0.90
    me10.8  0.995  0.000  0.000
    me10.9  0.10   0.99   0.00
    me10.10 0.7    0.2    0.3
    me15.1  0.90   0.95   0.00
    me15.2  0.875  0.500  0.500
    me15.3  0.99   0.90   0.95
    me15.4  0.00   0.75   0.75
    me15.5  0.9    0.9    0.2
    me15.6  0.00   0.00   0.95
    me15.7  0.00   0.87   0.90
    me15.8  0.995  0.000  0.000
    me15.9  0.10   0.99   0.00
    me15.10 0.7    0.2    0.3
    me15.11 0.99   0.90   0.95
    me15.12 0.00   0.87   0.90
    me15.13 0.10   0.99   0.00
    me15.14 0.7    0.2    0.3
    me15.15 0.90   0.95   0.00    
    me20.1  0.773  0.955  0.920
    me20.2  0.840  0.918  0.847
    me20.3  0.704  0.680  0.704
    me20.4  0.790  0.877  0.689
    me20.5  0.927  0.806  0.717
    me20.6  0.866  0.796  0.750
    me20.7  0.863  0.830  0.908
    me20.8  0.981  0.785  0.946
    me20.9  0.628  0.991  0.672
    me20.10 0.664  0.944  0.882
    me20.11 0.611  0.555  0.952
    me20.12 0.640  0.684  0.848
    me20.13 0.678  0.761  0.579
    me20.14 0.995  0.812  0.741
    me20.15 0.782  0.700  0.745
    me20.16 0.652  0.811  0.892
    me20.17 0.788  0.838  0.644
    me20.18 0.721  0.902  0.856
    me20.19 0.758  0.806  0.907
    me20.20 0.577  0.821  0.573
#           1      2      3      4      5      6
;   
Table PlcinU_all(case,l_,c_) #输入规定的进口杂质浓度
#           1      2      3      4      5      6
            1      2      3      4      5      6
    Ex1.1   1E+5    
    Ex2.1   200
    Ex2.2   1000
    Ex3.1   600
    Ex3.2   200
    Ex4.1   1E+5   1E+5
    Ex4.2   1E+5   1E+5
    Ex5.1   1E+5   1E+5   1E+5
    Ex5.2   1E+5   1E+5   1E+5
    Ex5.3   1E+5   1E+5   1E+5
    Ex6.1   1E+5   1E+5   1E+5
    Ex6.2   1E+5   1E+5   1E+5
    Ex6.3   1E+5   1E+5   1E+5
    Ex7.1   1E+5   1E+5   1E+5
    Ex7.2   1E+5   1E+5   1E+5
    Ex7.3   1E+5   1E+5   1E+5
    Ex8.1   1E+5   1E+5   1E+5   1E+5   1E+5   1E+5
    Ex8.2   1E+5   1E+5   1E+5   1E+5   1E+5   1E+5
    Ex8.3   1E+5   1E+5   1E+5   1E+5   1E+5   1E+5
    Ex9.1   1E+5   1E+5   1E+5   1E+5   1E+5   1E+5
    Ex9.2   1E+5   1E+5   1E+5   1E+5   1E+5   1E+5
    Ex9.3   1E+5   1E+5   1E+5   1E+5   1E+5   1E+5
    Ex9.4   1E+5   1E+5   1E+5   1E+5   1E+5   1E+5
    Ex9.5   1E+5   1E+5   1E+5   1E+5   1E+5   1E+5
    Ex10.1  400   
    Ex10.2  100         
    Ex10.3  50    
    Ex10.4  570    
    Ex10.5  100
    Ex11.1  400   
    Ex11.2  100         
    Ex11.3  50    
    Ex11.4  570    
    Ex11.5  100
    Ex11.6  30    
    Ex11.7  640
    Ex12.1  30   
    Ex12.2  100         
    Ex12.3  50    
    Ex12.4  227     
    Ex12.5  100
    Ex12.6  300     
    Ex12.7  12
    Ex12.8  970
    Ex12.9  20     
    Ex12.10 250
    Ex13.1  300   
    Ex13.2  10         
    Ex13.3  500    
    Ex13.4  570     
    Ex13.5  100
    Ex13.6  300     
    Ex13.7  200
    Ex13.8  47
    Ex13.9  200     
    Ex13.10 250
    Ex13.11 136   
    Ex13.12 50         
    Ex13.13 100    
    Ex13.14 270     
    Ex13.15 10
    Ex14.1  145    400 
    Ex14.2  110    90     
    Ex14.3  90     100
    Ex14.4  200    90
    Ex14.5  50     80
    Ex15.1  145    400    200
    Ex15.2  110    90     120
    Ex15.3  90     100    300
    Ex16.1  45     200 
    Ex16.2  110    90      
    Ex16.3  190    50
    Ex16.4  200    90  
    Ex16.5  60     500
    Ex16.6  20     190
#           1      2      3      4      5      6
    me4.1   1E+5   1E+5   1E+5
    me4.2   1E+5   1E+5   1E+5
    me4.3   1E+5   1E+5   1E+5
    me4.4   1E+5   1E+5   1E+5
    me10.1  1E+5   1E+5   1E+5
    me10.2  1E+5   1E+5   1E+5
    me10.3  1E+5   1E+5   1E+5
    me10.4  1E+5   1E+5   1E+5
    me10.5  1E+5   1E+5   1E+5
    me10.6  1E+5   1E+5   1E+5
    me10.7  1E+5   1E+5   1E+5
    me10.8  1E+5   1E+5   1E+5
    me10.9  1E+5   1E+5   1E+5
    me10.10 1E+5   1E+5   1E+5
    me15.1  1E+5   1E+5   1E+5
    me15.2  1E+5   1E+5   1E+5
    me15.3  1E+5   1E+5   1E+5
    me15.4  1E+5   1E+5   1E+5
    me15.5  1E+5   1E+5   1E+5
    me15.6  1E+5   1E+5   1E+5
    me15.7  1E+5   1E+5   1E+5
    me15.8  1E+5   1E+5   1E+5
    me15.9  1E+5   1E+5   1E+5
    me15.10 1E+5   1E+5   1E+5
    me15.11 1E+5   1E+5   1E+5
    me15.12 1E+5   1E+5   1E+5
    me15.13 1E+5   1E+5   1E+5
    me15.14 1E+5   1E+5   1E+5
    me15.15 1E+5   1E+5   1E+5   
    me20.1  1E+5   1E+5   1E+5
    me20.2  1E+5   1E+5   1E+5
    me20.3  1E+5   1E+5   1E+5
    me20.4  1E+5   1E+5   1E+5
    me20.5  1E+5   1E+5   1E+5
    me20.6  1E+5   1E+5   1E+5
    me20.7  1E+5   1E+5   1E+5
    me20.8  1E+5   1E+5   1E+5
    me20.9  1E+5   1E+5   1E+5
    me20.10 1E+5   1E+5   1E+5
    me20.11 1E+5   1E+5   1E+5
    me20.12 1E+5   1E+5   1E+5
    me20.13 1E+5   1E+5   1E+5
    me20.14 1E+5   1E+5   1E+5
    me20.15 1E+5   1E+5   1E+5
    me20.16 1E+5   1E+5   1E+5
    me20.17 1E+5   1E+5   1E+5
    me20.18 1E+5   1E+5   1E+5
    me20.19 1E+5   1E+5   1E+5
    me20.20 1E+5   1E+5   1E+5
#           1      2      3      4      5      6
;

#---Generalized pooling problem
Table FjU_all(case,j_)      # Maximum Capacity of sink 
             1      2
    me4      300.5  
    me10     300.5  
    me15     300.5
    me20     210    210
;
Table OCl_all(case,l_)    # me pool Operate Cost 
                1       2       3       4       5
    me4         1102.9  2895.2  1102.9  1102.9
    me10        3860.3  2895.2  1102.9  3860.3  3860.3
    me15        3860.3  2895.2  1102.9  3860.3  3860.3
    me20        3400.3  1962.3  2713.7  2472.0  3282.6                                    
       +        6       7       8       9       10
    me10        3860.3  2895.2  2895.2  1102.9  1102.9
    me15        3860.3  2895.2  2895.2  1102.9  1102.9
    me20        3024.2  2412.9  1537.0  3142.8  2389.4 
       +        11      12      13      14      15
    me15        2205.8  3184.7  1213.2  1213.2  2702.2
    me20        2730.9  3083.9  3343.6  2976.4  1852.5 
       +        16      17      18      19      20         
    me20        2311.4  3370.9  3333.8  2320.5  3287.3
; 

Table ICl_all(case,l_)   # me pool fixed Cost 
                1       2       3       4       5
    me4         13972   36676   13972   13972  
    me10        48901   36676   13972   48901   48901 
    me15        48901   36676   13972   48901   48901
    me20        39751   36156   38034   37430   39456
       +        6       7       8       9       10
    me10        48901   36676   36676   13972   13972
    me15        48901   36676   36676   13972   13972
    me20        38810   37282   35093   39107   37224 
       +        11      12      13      14      15
    me15        27944   40344   15369   15369   34231
    me20        38077   38960   39609   38691   35881
       +        16      17      18      19      20         
    me20        37029   39677   39585   37051   39468
; 
Table D_pipe_all(case,s_,m_) #Distance,m
             1     2     3     4    5    6    7    8    9    10    11   12   13   14   15   16   17   18    19    20    21    22     
    me4.1    0     80    210   190  75 
    me4.2    80    0     130   100  80 
    me4.3    210   130   0     110  160 
    me4.4    190   100   110   0    190
    me4.5    75    150   280   245  150
    me4.6    55    125   260   215  135
    me4.7    30    115   240   220  100
    me4.8    55    140   245   245  90
    me4.9    55    40    150   150  40
    me4.10   40    120   230   230  70
    me4.11   30    60    175   165  45
#            1     2     3     4    5    6    7    8    9    10    11   12   13   14   15   16   17   18    19    20    21    22     
    me10.1   0     20    40    50   70   70   100  160  230  190   120  
    me10.2   20    0     30    30   60   50   80   140  215  180   95 
    me10.3   40    30    0     40   80   60   80   140  210  190   75
    me10.4   50    30    40    0    40   15   50   110  180  150   85
    me10.5   70    60    80    40   0    25   50   110  180  120   120
    me10.6   70    50    60    15   25   0    30   100  170  130   90
    me10.7   100   80    80    50   50   30   0    60   130  100   80
    me10.8   160   140   140   110  110  100  60   0    70   100   95
    me10.9   230   215   210   180  180  170  130  70   0    110   160 
    me10.10  190   180   190   150  120  130  100  100  110  0     190
    me10.11  40    65    75    100  120  110  150  210  280  245   150 
    me10.12  15    40    55    75   90   90   125  180  260  215   135
    me10.13  40    35    30    65   100  85   115  170  240  220   100
    me10.14  85    80    55    100  140  120  140  180  245  245   90  
    me10.15  95    70    55    45   75   45   40   75   150  150   40
    me10.16  80    70    40    90   125  100  120  150  230  230   70
    me10.17  70    45    30    40   75   50   60   100  175  165   45    
#            1     2     3     4    5    6    7    8    9    10    11   12   13   14   15   16   17   18    19    20    21    22     
    me15.1   0     20    40    50   70   70   100  160  230  190   40   100  230  190  5    120
    me15.2   20    0     30    30   60   50   80   140  215  180   30   80   215  180  20   95
    me15.3   40    30    0     40   80   60   80   140  210  190   5    80   210  190  40   75
    me15.4   50    30    40    0    40   15   50   110  180  150   40   50   180  150  50   85
    me15.5   70    60    80    40   0    25   50   110  180  120   80   50   180  120  70   120
    me15.6   70    50    60    15   25   0    30   100  170  130   60   30   170  130  70   90
    me15.7   100   80    80    50   50   30   0    60   130  100   80   5    130  100  100  80
    me15.8   160   140   140   110  110  100  60   0    70   100   140  60   70   100  160  95
    me15.9   230   215   210   180  180  170  130  70   0    110   210  130  5    110  230  160
    me15.10  190   180   190   150  120  130  100  100  110  0     190  100  110  5    190  190
    me15.11  40    30    5     40   80   60   80   140  210  190   0    80   210  190  40   75
    me15.12  100   80    80    50   50   30   5    60   130  100   80   0    130  100  100  80
    me15.13  230   215   210   180  180  170  130  70   5    110   210  130  0    110  230  160
    me15.14  190   180   190   150  120  130  100  100  110  5     190  100  110  0    190  190
    me15.15  5     20    40    50   70   70   100  160  230  190   40   100  230  190  0    120
    me15.16  40    65    75    100  120  110  150  210  280  245   75   150  280  245  40   150
    me15.17  15    40    55    75   90   90   125  180  260  215   55   125  260  215  15   135
    me15.18  40    35    30    65   100  85   115  170  240  220   30   115  240  220  40   100
    me15.19  85    80    55    100  140  120  140  180  245  245   55   140  245  245  85   90
    me15.20  95    70    55    45   75   45   40   75   150  150   55   40   150  150  95   40
    me15.21  80    70    40    90   125  100  120  150  230  230   40   120  230  230  80   70
    me15.22  70    45    30    40   75   50   60   100  175  165   30   60   175  165  70   45
#            1     2     3     4    5    6    7    8    9    10    11   12   13   14   15   16   17   18    19    20    21    22      
    me20.1   0     3.9   7.9   11.8 15.8 19.7 23.7 27.6 31.6 35.5  39.5 43.4 47.4 51.3 55.3 59.2 63.2 67.1  71.0  75.0  79.0  90.1 
    me20.2   3.9   0     3.9   7.9  11.8 15.8 19.7 23.7 27.6 31.6  35.5 39.5 43.4 47.4 51.3 55.3 59.2 63.2  67.1  71.0  77.9  88.0
    me20.3   7.9   3.9   0     3.9  7.9  11.8 15.8 19.7 23.7 27.6  31.6 35.5 39.5 43.4 47.4 51.3 55.3 59.2  63.2  67.1  76.9  86.0
    me20.4   11.8  7.9   3.9   0    3.9  7.9  11.8 15.8 19.7 23.7  27.6 31.6 35.5 39.5 43.4 47.4 51.3 55.3  59.2  63.2  76.1  84.1
    me20.5   15.8  11.8  7.9   3.9  0    3.9  7.9  11.8 15.8 19.7  23.7 27.6 31.6 35.5 39.5 43.4 47.4 51.3  55.3  59.2  75.6  82.4
    me20.6   19.7  15.8  11.8  7.9  3.9  0    3.9  7.9  11.8 15.8  19.7 23.7 27.6 31.6 35.5 39.5 43.4 47.4  51.3  55.3  75.2  80.9
    me20.7   23.7  19.7  15.8  11.8 7.9  3.9  0    3.9  7.9  11.8  15.8 19.7 23.7 27.6 31.6 35.5 39.5 43.4  47.4  51.3  75.0  79.5
    me20.8   27.6  23.7  19.7  15.8 11.8 7.9  3.9  0    3.9  7.9   11.8 15.8 19.7 23.7 27.6 31.6 35.5 39.5  43.4  47.4  75.0  78.3
    me20.9   31.6  27.6  23.7  19.7 15.8 11.8 7.9  3.9  0    3.9   7.9  11.8 15.8 19.7 23.7 27.6 31.6 35.5  39.5  43.4  75.3  77.2
    me20.10  35.5  31.6  27.6  23.7 19.7 15.8 11.8 7.9  3.9  0     3.9  7.9  11.8 15.8 19.7 23.7 27.6 31.6  35.5  39.5  75.7  76.4
    me20.11  39.5  35.5  31.6  27.6 23.7 19.7 15.8 11.8 7.9  3.9   0    3.9  7.9  11.8 15.8 19.7 23.7 27.6  31.6  35.5  76.4  75.7
    me20.12  43.4  39.5  35.5  31.6 27.6 23.7 19.7 15.8 11.8 7.9   3.9  0    3.9  7.9  11.8 15.8 19.7 23.7  27.6  31.6  77.2  75.3 
    me20.13  47.4  43.4  39.5  35.5 31.6 27.6 23.7 19.7 15.8 11.8  7.9  3.9  0    3.9  7.9  11.8 15.8 19.7  23.7  27.6  78.3  75.0 
    me20.14  51.3  47.4  43.4  39.5 35.6 31.6 27.6 23.7 19.7 15.8  11.8 7.9  3.9  0    3.9  7.9  11.8 15.8  19.7  23.7  79.5  75.0
    me20.15  55.3  51.3  47.4  43.4 39.5 35.5 31.6 27.6 23.7 19.7  15.8 11.8 7.9  3.9  0    3.9  7.9  11.8  15.8  19.7  80.9  75.2 
    me20.16  59.2  55.3  51.3  47.4 43.4 39.5 35.5 31.6 27.6 23.7  19.7 15.8 11.8 7.9  3.9  0    3.9  7.9   11.8  15.8  82.4  75.6 
    me20.17  63.6  59.2  55.3  51.3 47.4 43.4 39.5 35.5 31.6 27.6  23.7 19.7 15.8 11.8 7.9  3.9  0    3.9   7.9   11.8  84.1  76.1 
    me20.18  67.1  63.2  59.2  55.3 51.3 47.4 43.4 39.5 35.5 31.6  27.6 23.7 19.7 15.8 11.8 7.9  3.9  0     3.9   7.9   86.0  76.9   
    me20.19  71.0  67.1  63.2  59.2 55.3 51.3 47.4 43.4 39.5 35.5  31.6 27.6 23.7 19.7 15.8 11.8 7.9  3.9   0     3.9   88.0  77.9 
    me20.20  75.0  71.0  67.1  63.2 59.2 55.3 51.3 47.4 43.4 39.4  35.5 31.6 27.6 23.7 19.7 15.8 11.8 7.9   3.9   0     90.1  79.0
    me20.21  75.0  75.1  75.4  75.9 76.6 77.6 78.7 79.9 81.4 83.0  84.8 86.7 88.7 90.9 93.2 95.6 98.0 100.6 103.3 106.1 152.1 158.1 
    me20.22  76.0  75.5  75.1  75.0 75.1 75.3 75.8 76.5 77.4 78.4  79.7 81.1 82.7 84.4 86.3 88.4 90.5 92.8  95.1  97.6  150.5 154.6
    me20.23  79.0  77.9  76.9  76.1 75.6 75.2 75.0 75.0 75.3 75.7  76.4 77.2 78.3 79.5 80.9 82.4 84.1 86.0  88.0  90.1  150.0 152.1
    me20.24  83.8  82.2  80.6  79.3 78.1 77.1 76.3 75.6 75.2 75.0  75.0 75.2 75.6 76.3 77.1 78.1 79.3 80.6  82.2  83.8  150.5 150.5
    me20.25  90.1  88.0  86.0  84.1 82.4 80.9 79.5 78.3 77.2 76.4  75.7 75.3 75.0 75.0 75.2 75.6 76.1 76.9  77.9  79.0  152.1 150.0
    me20.26  97.6  95.1  92.8  90.5 88.4 86.3 84.4 82.7 81.1 79.7  78.5 77.4 76.5 75.8 75.3 75.1 75.0 75.1  75.5  76.0  154.6 150.5
    me20.27  106.1 103.3 100.6 98.0 95.6 93.2 90.9 88.7 86.7 84.8  83.0 81.4 79.9 78.6 77.6 76.6 75.9 75.4  75.1  75.0  158.1 152.1                                                                                       
;  
$offecho
$include Data_input
#======================Step 0： 模型,数据和参数设置导入=====================
WTN_gpp.solprint = 2;
WTN_gpp_RP_PN.solprint = 2;
WTN_gpp_RP_PN_Abt.solprint = 2;
WTN_gpp_RP_PN_Abt.optfile = 2;#2对应的是模型的Abt属性文件，如果两个模型都使用1，则可能会造成一个属性文件包括不同的属性，会冲突

*WTN.optfile = 0;
*WTN_gpp.optfile = 1;
*file opt /Gurobi.opt/;
*putclose opt 'Nonconvex 2';
#key parameters for GO control
Scalar
    YGOstep1 'implement step 1 in GO or not' /0/,#默认没必要用，除非要执行边界紧缩或者上下定界模型交互
    YGOstep2_3 'implement step 2&3 in GO or not' /1/
    YGOstep4 'implement step 4(abt) in GO or not'/0/
    Gap 'current gap' /+Inf/, Gap_tol 'Gap tolerance' /1E-3/
    Dynamic_factor 'added partitions adjust factor '/0.1/#越大每轮增加的分割变量越少，等于1相当于大于均值的, 目前看取中小规模取0.1是最佳的，大和特大规模问题则建议取1，其实影响并不大，都取1就好
    Gap_rp_tol  'rel_gap for RP'/0/#默认RP求到最优解 误差为0
;
#--- Data Setting for GO (Step 1)---
Set itermul 'multistart total rounds'/1*100/;
Scalar Time_start_1,Time_start_2;
#多起点停止辅助集合和参数
Set sol_ /1*1000/;
Set sol(sol_); sol(sol_) = no;
Parameter
    Objvalcou(sol_) '第sol局部解对应的目标值'
    Freq(sol_)  '第sol局部解对应的出现次数'
    Ysol_add  '判断是否增加了频数'
;
    Objvalcou(sol_) = 0;
    Freq(sol_) = 0;  
Set itermul_b '冒泡排序迭代轮次索引'/1*1000/;
Scalar Aul '冒泡排序辅助参数';
Scalar
    NitermulU 'round upper bound for multistart'/10/
;
#--- Data Setting for GO (Step 2 and 3)---
Sets
    itergo 'iteration index for GO rounds'/1*100/
    qpmr 'PMR 分割点数总集合' /1*1001/
    l_c_qpmr(l_,c_,qpmr) '不同plcout对应的分割点数'
;
Alias(qpmr_a, qpmr),(itergo_a,itergo);
Scalar
    #LB 'current lower bound' /-Inf/, UB 'current upper bound' /+Inf/,#为了避免abt数值问题，别给正负无穷大了
    LB 'current lower bound' /-1E+10/, UB 'current upper bound' /+1E+10/,
    NitergoU 'round upper bound for GO'/50/ #Step 2
    Epsilon_LB 'a small numeric value to avoid numeric problem'/1E-8/
    Nd_dynamic 'added partitions per round'/1/
    Ub_solve_switch_gap '切换原问题Dicopt求解方式的gap阈值' /1E-2/# 1%
    Total_timeU '总求解时间上限,s' /36000/
    Count_switch '计算原问题Dicopt求解模式切换的轮次' /0/
;
Parameter
    Report_go(itergo, *)
    Npmr_U(l_,c_) 'PMR 分割区间数'
    Plcoutq_pmr(l_,c_,qpmr) '分割点浓度值'
    Fsmc_devi(s_,m_,c_) 'devation of fsmc in RP'
    Pclout_BLsum_devi(l_,c_) 'total devation of fsmc in RP about pclout'
    Pclout_BLsum_devi_Ave 'nonzero average of Pclout_BLsum_devi'
    Npmr_LSol(l_,c_) '下定界模型RPyplcout_pmr(l,c,npmr)解所在区间索引的值'
    Plcout_LSol(l_,c_) '下定界模型Plcout(l,c)解' 
;
file dicopt_opt /dicopt.opt/;
#--- Data Setting for GO (Step 4)---
Sets
    l_c_abt(l_,c_) 'candidate variables for abt'
    iter_abt  '执行不同分割区间数对应的轮次，并不是总的分割变量轮次'/1*100/
;
Alias (l,l_b),(c,c_b),(npmr,npmr_a);#边界紧缩中使用的索引
Scalar
    NiterabtU 'round upper bound for abt'/50/
    Lo_partitionNum '统计平均分割区间数的阈值(不包括)'/3/
    Ave_partiions_to_step4 'average partitions for bigger than Lo_partitionNum to enter step 4'/5/
    Total_partiions_to_step4  'total number of partitions for enter step 4'/50/
    Count_bt 'Abt过程紧缩迭代更新的紧缩后子区间指针 1,2,...'
    Interval 'Abt过程紧缩子区间指针'
    Time_start_abt 'Abt起始时刻'#注意Abt是基于每轮itergo记录的总时长
    Abt_Count '总紧缩求解次数' /0/
    #Test, AAA , BBB, CCC "Npmr_U",DDD "Npmr_USol", EEE "Npmr_LSol", FFF "current abt variable"
;
Parameter
    Npmr_USol(l_,c_) '当前上界解对应的松弛模型所在子区间RPyplcout_pmr(l,c,npmr)解所在区间索引的值'
    Yplcout_pmr_Bt(l_,c_,npmr) '紧缩后是否存在，如果还存在对应为1，否则为0'
    Npmr_U_temp(l_,c_) 'PMR 分割区间数,暂存值'
;
File bbcontrol /Gurobi.op2/;

#Parameters for bound update and tighting
Parameters
    PlcoutUp_temp(l_,c_) 'PlcoutUp暂存值'
    PlcoutLo_temp(l_,c_)'PlcoutLo暂存值'
    YlUp_temp(l_) 'YlUp暂存值'
    YlLo_temp(l_) 'YlLo暂存值'
    FsmUp_temp(s_,m_) 'FsmUp暂存值'
    FsmLo_temp(s_,m_)'FsmLo暂存值'
    Caseindex '案例索引'
;

#
loop(case$(ord(case)=18),
#loop(case,
    Caseindex = ord(case);
    display '=======================================================',Caseindex;
    Ygpp$(ord(case)<17 or ord(case)>20) = 0;#非me算例
    Ygpp$(ord(case)>=17 and ord(case)<=20) = 1;#me算例
    Yunitselfrecycled$(ord(case)<17 or ord(case)>20) = 1;
    Yunitselfrecycled$(ord(case)>=17 and ord(case)<=20) = 0;
    
    Ymeyer20$(ord(case)=20) = 1;#meyer20 case limited number of treatment units
    NlU_meyer20 = 3;
    
    # extract domains for current problem(optional)
    i(i_) = Fi_all(case,i_);
    j(j_) = sum(c_$PjcU_all(case,j_,c_), 1);
    l(l_) = sum(c_$PlcinU_all(case,l_,c_), 1); 
    c(c_) = sum(i_$Pic_all(case,i_,c_), 1);#集合的值只有真假也就是0,1
    s(s_) = yes$(ord(s_) <= (card(i) + card(l))); #分离器个数等于废水源数量+水处理单元数量
    m(m_) = yes$(ord(m_) <= (card(j) + card(l))); #混合器个数等于水处理单元数量+废水排放阱数量
    #s集合元素中按前面的s为sl,后面的是为si
    sl(s_) = yes$(ord(s_) <= card(l));
    si(s_) = yes$((ord(s_) > card(l)) and (ord(s_) <= card(s)));
    #m集合元素中按前面的m为ml,后面的是为mj
    ml(m_) = yes$(ord(m_) <= card(l));
    mj(m_) = yes$((ord(m_) > card(l)) and (ord(m_) <= card(m)));
    #二维子集合是定义，指定拓扑关系
    lout(l_,s_) =  yes$((ord(l_) = ord(s_)) and (ord(l_) <= card(l)));
    lin(m_,l_) =   yes$((ord(m_) = ord(l_)) and (ord(l_) <= card(l)));
    iout(i_, s_) = yes$((ord(i_) + card(sl) = ord(s_)) and (ord(i_) <= card(i)));
    jin(m_,j_) =   yes$((ord(m_) - card(l) = ord(j_)) and (ord(j_) <= card(j)));
    #自循环过程流股集合
    ssm(s_,m_) = yes$(sl(s_) and ml(m_) and (ord(s_)=ord(m_)));
    #允许连接的流股集合
    asm(s_,m_)$((1-Yunitselfrecycled)) = yes$(s(s_) and m(m_) and (not ssm(s_,m_))); 
    asm(s_,m_)$(Yunitselfrecycled) = yes$(s(s_) and m(m_)); 
 
    # extract data
    Kloopt = 3;
    Fi(i) = Fi_all(case,i);            
    Pic(i,c) = Pic_all(case,i,c)*1E-3;#单位换算为10^3 ppm
    Rlc(l,c) = Rlc_all(case,l,c);
    # ----------------!!!!!!!!!---------------
    PlcinU(l,c) = min(PlcinU_all(case,l,c)*1E-3, smax(i, Pic(i,c)));#单位换算为10^3 ppm  #这个要同时考虑输入和拓扑 
    PjcU(j,c) = PjcU_all(case,j,c)*1E-3;#单位换算为10^3 ppm
    OCl(l)$Ygpp = OCl_all(case,l);
    ICl(l)$Ygpp = ICl_all(case,l);
    D_pipe(s,m)$(asm(s,m) and Ygpp) = D_pipe_all(case,s,m); 
    FjU(j)$Ygpp = FjU_all(case,j);
    #display i, j, l, c, s, si, sl, m, ml, mj, lin, lout, iout, jin;
    #display Fi, Pic,  Rlc, PlcinU, PjcU, FjU;
    #============ hard bound============
    if(Ygpp, #池问题可能包括多个环境阱，有容量限制
        fsm.Up(si,mj)$asm(si,mj) = min(sum(i$iout(i,si), Fi(i)), smin((j,i,c)$(iout(i,si) and (Pic(i,c) > 0) and jin(mj,j)), sum(ia, Fi(ia))*PjcU(j,c)/Pic(i,c)), sum(j$jin(mj,j), FjU(j)));  #fij_UP
        fsm.Up(sl,mj)$(asm(sl,mj) and not ssm(sl,mj)) = min(sum(i, Fi(i)), sum(j$jin(mj,j), FjU(j))); #flj_UP ,       
    else
        fsm.Up(si,mj)$asm(si,mj) = min(sum(i$iout(i,si), Fi(i)), smin((j,i,c)$(iout(i,si) and (Pic(i,c) > 0) and jin(mj,j)), sum(ia, Fi(ia))*PjcU(j,c)/Pic(i,c)));  #fij_UP
        fsm.Up(sl,mj)$(asm(sl,mj) and not ssm(sl,mj)) = sum(i, Fi(i)); #flj_UP    
    );
    fsm.Up(si,ml)$asm(si,ml) = sum(i$iout(i,si), Fi(i));#fil_UP
    fsm.Up(sl,ml)$(asm(sl,ml) and not ssm(sl,ml)) = sum(i, Fi(i)); #fll_UP
    fsm.Up(sl,ml)$(asm(sl,ml) and ssm(sl,ml)) = sum(i, Fi(i))*Kloopt;##fll'_UP_recycle
    fl.Up(l)$(Yunitselfrecycled) = Kloopt*sum(i, Fi(i));#考虑循环的水处理单元的上界
    fl.Up(l)$(1-Yunitselfrecycled) = sum(i, Fi(i));#不考虑循环的水处理单元的上界
    plcout.Up(l,c) = (1-Rlc(l,c))*PlcinU(l,c);#水处理单元出口浓度上界，跟最大进口浓度上界直接相关
    #display fsm.Up, PlcinU, plcout.Up;
    #display fl.Up;

    #======================== Global optimization algorithm ================================
    #---------------------------------------------------------------------------------------
    #------------------------ GO-Step 1：multistart local optimization ---------------------
    #---------------------------------------------------------------------------------------
    Time_start_1 = timeExec;
    if(YGOstep1,
        loop(itermul$(ord(itermul)<=NitermulU),
            plcout.L(l,c) = uniform(plcout.Lo(l,c), plcout.Up(l,c));
            fsm.L(s,m)$asm(s,m) = uniform(fsm.Lo(s,m), fsm.Up(s,m));
            fl.L(l) = uniform(fl.Lo(l), fl.Up(l));
            yl.L(l)$Ygpp = uniform(0,1);
            ysm.L(s,m)$(asm(s,m) and Ygpp) = uniform(0,1);
            WTN.solprint = 2;
            #
            WTN_gpp.solprint = 2;
            if(Ygpp,
                Solve WTN_gpp using MIQCP minimizing objval;
            else
                Solve WTN using QCP minimizing objval;        
            );
            #Report_mul(itermul,'NLP_stat')= WTN.modelstat;
            #Report_mul(itermul,'Solving_Time') = WTN.ResUsd;
            if(WTN.modelstat = 1 or WTN.modelstat = 2 or WTN.modelstat = 7 or WTN.modelstat = 8 or
               WTN_gpp.modelstat = 1 or WTN_gpp.modelstat = 2 or WTN_gpp.modelstat = 7 or WTN_gpp.modelstat = 8,
                Report_mul(itermul,'Objval_local')= objval.L;
                #------------step 1.1:对局部解进行统计----
                Ysol_add = 0;
                loop(sol,
                    if(abs(objval.L - Objvalcou(sol))<1E-3,#如果没发现新解
                        Freq(sol) = Freq(sol) + 1;
                        Ysol_add = 1;
                    );
                );
                if(Ysol_add = 0, #如果发现新的局部解
                    sol(sol_)$(ord(sol_) = (card(sol)+1)) = yes;
                    Objvalcou(sol_)$(ord(sol_) = card(sol)) = objval.L;
                    Freq(sol_)$(ord(sol_) = card(sol)) = 1; 
                );
                #display sol, Objvalcou, Freq;
                
                #------------step 1.2：更新最优解--------- 
                if(objval.L < UB,#更新最优解
                    UB = objval.L; 
                    Fsm_USol(s,m) = fsm.L(s,m);   
                    Fl_USol(l) = fl.L(l);
                    Plcout_USol(l,c)$(Fl_USol(l)>0) = plcout.L(l,c)*1E+3; #注意有的f变量优化结果为0,浓度值没意义可以按0算     
                    Plcin_USol(l,c)$(Fl_USol(l)>0) = Plcout_USol(l,c)/(1- Rlc(l,c)); #注意有的f变量优化结果为0,浓度值没意义可以按0算
                    Pjc_USol(j,c) = (sum((i,si,mj)$(iout(i,si) and jin(mj,j)), fsm.L(si,mj)*Pic(i,c)) +
                                     sum((l,sl,mj)$(lout(l,sl) and jin(mj,j)), fsm.L(sl,mj)*plcout.L(l,c)))/sum((s,mj)$jin(mj,j), fsm.L(s,mj))*1E+3; 
                    Fil_USol(i,l) = sum((si,ml)$(iout(i,si) and lin(ml,l)), fsm.L(si,ml));
                    Fij_USol(i,j) = sum((si,mj)$(iout(i,si) and jin(mj,j)), fsm.L(si,mj)); 
                    Fll_USol(l,la)= sum((sl,ml)$(lout(l,sl) and lin(ml,la)), fsm.L(sl,ml));
                    Flj_USol(l,j) = sum((sl,mj)$(lout(l,sl) and jin(mj,j)), fsm.L(sl,mj));
                    
                );
            else
                Report_mul(itermul,'Objval_local')= +INF;     
            );
            #变量当前值清理(赋值为默认值零,后续初始化基于LP模型的结果)
            fsm.L(s_,m_) = 0;
            fl.L(l_) = 0;
            plcout.L(l_,c_) = 0;
            yl.L(l_) = 0;
            ysm.L(s_,m_) = 0;
        );
        #------------step 1.3: 统计结果重新排序(冒泡）---------
        loop(itermul_b$(ord(itermul_b) <= (card(sol)-1)),#一共执行N-1轮迭代
            loop(sol_$(ord(sol_) <= (card(sol) - ord(itermul_b))),
                if(Objvalcou(sol_) > Objvalcou(sol_+1),#两两交换，从前到后执行,小的放前面,第itermul轮，执行N-itermul次
                    Aul = Objvalcou(sol_);
                    Objvalcou(sol_) = Objvalcou(sol_+1);
                    Objvalcou(sol_+1) = Aul;
                    Aul = Freq(sol_);
                    Freq(sol_) = Freq(sol_+1);
                    Freq(sol_+1) = Aul;
                );
            );
        );
        #display "======================================step 1: Result display =====================================";
        #display sol, Objvalcou, Freq;#这块得加个冒泡排序，而且发现个规律，越接近全局最优解的，出现的频数越高，证明了方法的有效性！！！就是该方法很大概率发现近最优解
        #display 'Current solutions:',UB, Fl_USol, Fsm_USol, Plcin_USol, Plcout_USol, Pjc_USol, Fil_USol, Fij_USol, Fll_USol, Flj_USol;
        Report(case,'Mul_timeuse') = timeExec-Time_start_1;
    );
   
    #求解非分割RP
    #Solve WTN_gpp_RP_P1 using MIP minimizing objval;
    #---全均匀分割---
*    Npmr_U(l,c) = 1;
*    l_c_npmr(l,c,npmr) = yes$(ord(npmr) <= Npmr_U(l,c));
*    l_c_qpmr(l,c,qpmr) = yes$(ord(qpmr) <= (Npmr_U(l,c)+1));
*    Plcoutq_pmr(l,c,qpmr)$l_c_qpmr(l,c,qpmr) = plcout.Lo(l,c)+(plcout.Up(l,c)-plcout.LO(l,c))/Npmr_U(l,c)*(ord(qpmr) - 1);   
*    PlcoutnUp_pmr(l,c,npmr)$l_c_npmr(l,c,npmr) = sum(qpmr$(ord(qpmr) = (ord(npmr) + 1)), Plcoutq_pmr(l,c,qpmr));
*    PlcoutnLo_pmr(l,c,npmr)$l_c_npmr(l,c,npmr) = sum(qpmr$(ord(qpmr) = ord(npmr)), Plcoutq_pmr(l,c,qpmr));
*    display l_c_npmr, PlcoutnUp_pmr, PlcoutnLo_pmr; 
*    Solve WTN_gpp_RP_PN using MIP minimizing objval;
    Time_start_2 = timeExec;
    if(YGOstep2_3,
        #先基于当前UB解初始化RP,在基于LB解初始化P,不断迭代，暂时不考虑松弛原问题二元变量和边界紧缩部分---暂未执行
        loop(itergo$(ord(itergo)<=NitergoU),
        #---------------------------------------------------------------------------------------
        #------------------------ GO-Step 2：solve RP, P and update results --------------------
        #---------------------------------------------------------------------------------------
            #---LBsolve&renew---
            if(ord(itergo)=1, #Initialization
                Npmr_U(l,c) = 1; 
                l_c_npmr(l,c,npmr) = yes$(ord(npmr) <= Npmr_U(l,c));
                l_c_qpmr(l,c,qpmr) = yes$(ord(qpmr) <= (Npmr_U(l,c)+1));
                Plcoutq_pmr(l,c,'1') = plcout.Lo(l,c);
                Plcoutq_pmr(l,c,'2') = plcout.Up(l,c);   
                PlcoutnUp_pmr(l,c,npmr)$l_c_npmr(l,c,npmr) = sum(qpmr$(ord(qpmr) = (ord(npmr) + 1)), Plcoutq_pmr(l,c,qpmr));
                PlcoutnLo_pmr(l,c,npmr)$l_c_npmr(l,c,npmr) = sum(qpmr$(ord(qpmr) = ord(npmr)), Plcoutq_pmr(l,c,qpmr));    
            );
            #display l_c_npmr, PlcoutnUp_pmr, PlcoutnLo_pmr;
            WTN_gpp_RP_PN.optcr = Gap_rp_tol;
            Solve WTN_gpp_RP_PN using MIP minimizing objval;
            if((WTN_gpp_RP_PN.modelstat =1 or WTN_gpp_RP_PN.modelstat =8) and (objval.l >= (LB - Epsilon_LB )),
                LB = objval.l;
                #Npmr_LSol(l,c) = sum(npmr$(yplcout_pmr.L(l,c,npmr)>Smallvalue and l_c_npmr(l,c,npmr)), ord(npmr));#这个有数值问题
                Plcout_LSol(l,c) = plcout.L(l,c);
                Npmr_LSol(l,c) = sum(npmr$(PlcoutnUp_pmr(l,c,npmr) >= Plcout_LSol(l,c)
                                           and PlcoutnLo_pmr(l,c,npmr) <= Plcout_LSol(l,c)
                                           and l_c_npmr(l,c,npmr)), ord(npmr));
            );
            #display  Npmr_U,Npmr_LSol, yplcout_pmr.L; 
            #bi_pn.l(l,c,npmr) = 0;#对于变量域变化的，先清零，否则影响后续的参数赋值
            #---Calculate auxiliary parameters
            Fsmc_devi(sl,m,c)$asm(sl,m) = abs(fsmc.L(sl,m,c) - sum(l$lout(l,sl), plcout.L(l,c))*fsm.L(sl,m));
            Pclout_BLsum_devi(l,c) = sum((sl,m)$(asm(sl,m) and lout(l,sl)), Fsmc_devi(sl,m,c));
            Pclout_BLsum_devi_Ave = sum((l,c), Pclout_BLsum_devi(l,c))/sum((l,c)$(Pclout_BLsum_devi(l,c)>0), 1);
            #display Fsmc_devi, Pclout_BLsum_devi, Pclout_BLsum_devi_Ave;
           
            #---UBsolve&renew---
            if(Gap <= Ub_solve_switch_gap and Count_switch=0, #如果gap很小但是不收敛，则切换dicopt初始NLP求解为固定拓扑结构为松弛解，这个应该只执行一次
                Count_switch = ord(itergo);
                WTN_gpp.optfile = 1;
                putclose dicopt_opt 'relaxed  2';
                display 'Dicopt switch to mode 2', Count_switch;
            );
            Solve WTN_gpp using MIQCP minimizing objval;
            if((WTN_gpp.modelstat = 1 or WTN_gpp.modelstat = 2 or WTN_gpp.modelstat = 7 or WTN_gpp.modelstat = 8) and (objval.L < UB),#更新最优解
                UB = objval.L;
                Fsm_USol(s,m) = fsm.L(s,m);   
                Fl_USol(l) = fl.L(l);
                Plcout_USol(l,c)$(Fl_USol(l)>0) = plcout.L(l,c)*1E+3; #注意有的f变量优化结果为0,浓度值没意义可以按0算     
                Plcin_USol(l,c)$(Fl_USol(l)>0) = Plcout_USol(l,c)/(1- Rlc(l,c)); #注意有的f变量优化结果为0,浓度值没意义可以按0算
                Pjc_USol(j,c) = (sum((i,si,mj)$(iout(i,si) and jin(mj,j)), fsm.L(si,mj)*Pic(i,c)) +
                                 sum((l,sl,mj)$(lout(l,sl) and jin(mj,j)), fsm.L(sl,mj)*plcout.L(l,c)))/sum((s,mj)$jin(mj,j), fsm.L(s,mj))*1E+3; 
                Fil_USol(i,l) = sum((si,ml)$(iout(i,si) and lin(ml,l)), fsm.L(si,ml));
                Fij_USol(i,j) = sum((si,mj)$(iout(i,si) and jin(mj,j)), fsm.L(si,mj)); 
                Fll_USol(l,la)= sum((sl,ml)$(lout(l,sl) and lin(ml,la)), fsm.L(sl,ml));
                Flj_USol(l,j) = sum((sl,mj)$(lout(l,sl) and jin(mj,j)), fsm.L(sl,mj));
            );
            #更新上界解所在RP的子区间
            Npmr_USol(l,c) = sum(npmr$(PlcoutnUp_pmr(l,c,npmr) >= Plcout_USol(l,c)*1E-3 and PlcoutnLo_pmr(l,c,npmr) <= Plcout_USol(l,c)*1E-3 and l_c_npmr(l,c,npmr)), ord(npmr));
            
            #---Result and gap update and optimilaty check---
            Gap=abs((UB-LB)/UB);
            Report_go(itergo, 'LB') = LB;
            Report_go(itergo, 'UB') = UB;
            Report_go(itergo, 'Gap') = Gap;
            Report_go(itergo, 'Partitions') = sum((l,c), Npmr_U(l,c));                                 
            Report_go(itergo, 'Solving Time RP') = WTN_gpp_RP_PN.ResUsd;
            Report_go(itergo, 'Solving Time P') = WTN_gpp.ResUsd;
            #Report_go(itergo, 'Solving Time Total P+RP') = sum(itergo_a$(ord(itergo_a)<=ord(itergo)),Report_go(itergo_a, 'Solving Time RP')+Report_go(itergo_a, 'Solving Time P'));
            if(Gap <= Gap_Tol, #是否满足gap
                Report_go(itergo, 'TotalTime234') = timeExec-Time_start_2;  
                #display '============================================================================================================================'
                display 'the optimal soution is found!';
                display 'Global solutions:',UB, LB, Gap, Fl_USol, Fsm_USol, Plcin_USol, Plcout_USol, Pjc_USol, Fil_USol, Fij_USol, Fll_USol, Flj_USol;
                #display '============================================================================================================================='
                #display  Npmr_U;
                putclose dicopt_opt 'relaxed 1';#还原
                WTN_gpp.optfile = 0;#还原
                putclose bbcontrol 'mipstopexpr resusd >= 10000000';#还原
                WTN_gpp_RP_PN_Abt.optfile = 0;#还原
                break;#可以跳出多层循环
            elseif timeExec>Total_timeU,#最大求解时间达到
                Report_go(itergo, 'TotalTime234') = timeExec-Time_start_2;  
                #display '============================================================================================================================'
                display 'maximum total time is reached!';
                #display  Npmr_U;
                putclose dicopt_opt 'relaxed 1';#还原
                WTN_gpp.optfile = 0;#还原
                putclose bbcontrol 'mipstopexpr resusd >= 10000000';#还原
                WTN_gpp_RP_PN_Abt.optfile = 0;#还原
                break;#可以跳出多层循环
            );
            

            #---------------------------------------------------------------------------------------
            #------------------------ GO-Step 4：Adaptive bounds contraction  ----------------------
            #---------------------------------------------------------------------------------------
            Time_start_abt = timeExec;
            if(YGOstep4,
                if(sum((l_b,c_b)$(Npmr_U(l_b,c_b)>Lo_partitionNum), Npmr_U(l_b,c_b)) >= Ave_partiions_to_step4*sum((l_b,c_b)$(Npmr_U(l_b,c_b)>Lo_partitionNum), 1)
                   and sum((l_b,c_b)$(Npmr_U(l_b,c_b)>Lo_partitionNum), 1) > 0 and (sum((l_b,c_b), Npmr_U(l_b,c_b)) >= Total_partiions_to_step4)
                   #                   and ord(itergo) = 7
                   ,
                   #执行Abt的条件基于大于临近分割区间的区间数平均值以及总的分割区间数
                    #每轮GO紧缩对应的UB是不变的,所以不用每次紧缩都更新，而是每轮GO
                    putclose bbcontrol 'mipstopexpr objest > ' (round(Ub,5)+0.00001):0:5 ' || objval < ' (round(Ub,5)-0.00001):0:5;#圆整会出现舍去的情况，为保证参考值合理，额外加一个小值                   
                    #display Npmr_U, Npmr_USol, Npmr_LSol, Plcoutq_pmr; 
                    #------------ Step4-1 ---------------
                    l_c_abt(l_b,c_b) = yes;#初始化候选紧缩的集合
                    Npmr_U_temp(l_b,c_b) = Npmr_U(l_b,c_b);
                    #display l_c_abt; 
                    #执行紧缩的变量为候选变量中分割区间数最大的,并且是其中偏差值最大的
                    loop(iter_abt$(ord(iter_abt)<=NiterabtU),
                        loop((l_b,c_b)$(l_c_abt(l_b,c_b) and Npmr_U(l_b,c_b) = smax(l_c_abt(la,ca), Npmr_U(la,ca))
                                        and Npmr_U(l_b,c_b)>Lo_partitionNum),#限制紧缩的变量为：分割区间数大于限制值
                            if(Pclout_BLsum_devi(l_b,c_b) = smax(l_c_abt(la,ca)$(Npmr_U(la,ca) = Npmr_U(l_b,c_b)), Pclout_BLsum_devi(la,ca)),
                                # Step 4.1 ------ bound contraction implementation !!!!!(基于保守策略的紧缩方法)
                                Yplcout_pmr_Bt(l_b,c_b,npmr)$l_c_npmr(l_b,c_b,npmr)=1;#由于可能进行多轮紧缩，所以需要先重置为1
                                #基于RP解和P解位置判断先紧缩下边界还是上边界（看参考区间两侧的剩余区间数多少）
                                #display Plcout_LSol,plcout.Up,plcout.Lo;
                                #如果上下界解在整个紧缩区间两侧，则跳过该变量紧缩
                                if(max(Npmr_USol(l_b,c_b),Npmr_LSol(l_b,c_b))-min(Npmr_USol(l_b,c_b),Npmr_LSol(l_b,c_b))+1 = Npmr_U(l_b,c_b),
                                    l_c_abt(l_b,c_b) = no;#此时的(l_b,c_b)是确定的
                                    #
                                    display "========================================";
                                    #
                                    display l_c_abt; 
                                    #
                                    display "skip the abt for this variable";
                                    break;
                                );
                                #只紧缩远端子区间
                                if((Npmr_U(l_b,c_b)-max(Npmr_USol(l_b,c_b),Npmr_LSol(l_b,c_b)))
                                   >=(min(Npmr_USol(l_b,c_b),Npmr_LSol(l_b,c_b))-1),
                                    #display "========================================";
                                    #display "Ub domain is tightened";                                   
                                    for(Interval = Npmr_U(l_b,c_b) downto (max(Npmr_USol(l_b,c_b),Npmr_LSol(l_b,c_b))+1) by 1,#是downto,不是to,先到二者较大的
                                        yplcout_pmr.fx(l_b,c_b,npmr)$(ord(npmr)=Interval) = 1;#直接固定子区间为待消减的区间，不用更新边界
                                        
                                        #暂存边界值
                                        PlcoutUp_temp(l_b,c_b) = plcout.Up(l_b,c_b); 
                                        PlcoutLo_temp(l_b,c_b) = plcout.Lo(l_b,c_b);
                                        YlUp_temp(l_b) = yl.Up(l_b); 
                                        YlLo_temp(l_b) = yl.Lo(l_b); 
                                        FsmUp_temp(sl,mj)$(asm(sl,mj) and not ssm(sl,mj) and lout(l_b,sl)) = fsm.Up(sl,mj); 
                                        FsmUp_temp(sl,ml)$(asm(sl,ml) and not ssm(sl,ml) and lout(l_b,sl)) = fsm.Up(sl,ml);
                                        FsmUp_temp(sl,ml)$(asm(sl,ml) and not ssm(sl,ml) and lin(ml,l_b)) = fsm.Up(sl,ml);
                                        #display "before update";  
                                        #display plcout.Up, plcout.Lo, fsm.Up, yl.Lo;
                                        #增加对变量域的更新和更新相关其他变量的边界更新
                                        plcout.Up(l_b,c_b) = sum(npmr$(ord(npmr)=Interval), PlcoutnUp_pmr(l_b,c_b,npmr));
                                        plcout.Lo(l_b,c_b) = sum(npmr$(ord(npmr)=Interval), PlcoutnLo_pmr(l_b,c_b,npmr));
                                        #更新y
                                        if(plcout.Lo(l_b,c_b)>1E-3, yl.fx(l_b)=1);
                                        #更新flj,fll-lout,fll-min
                                        fsm.Up(sl,mj)$(asm(sl,mj) and not ssm(sl,mj) and lout(l_b,sl)) =
                                            (min(sum(i, Fi(i)), sum(j$jin(mj,j), FjU(j))))*sum(j$jin(mj,j),PjcU(j,c_b))/max(sum(j$jin(mj,j),PjcU(j,c_b)), plcout.Lo(l_b,c_b)); #flj_UP 
                                        fsm.Up(sl,ml)$(asm(sl,ml) and not ssm(sl,ml) and lout(l_b,sl)) =
                                            sum(i, Fi(i))*sum(la$lin(ml,la),plcout.Up(la,c_b))/max(sum(la$lin(ml,la),plcout.Up(la,c_b)), plcout.Lo(l_b,c_b)*sum(la$lin(ml,la),(1-Rlc(la,c_b)))); #fll_UP-lout                                         
                                        fsm.Up(sl,ml)$(asm(sl,ml) and not ssm(sl,ml) and lin(ml,l_b)) =
                                            sum(i, Fi(i))*plcout.Up(l_b,c_b)/max(plcout.Up(l_b,c_b), sum(la$lout(la,sl), plcout.Lo(la,c_b))*(1-Rlc(l_b,c_b))) ; #fll_UP-min                                                                              
                                        #display "after update";  
                                        #display plcout.Up, plcout.Lo, fsm.Up, yl.Lo;


                                        Solve WTN_gpp_RP_PN_Abt using MIP minimizing objval;
                                        Abt_Count = Abt_Count + 1;
                                        

                                        #还原变量边界避免影响其他问题(基于同样的变量)
                                        plcout.Up(l_b,c_b) = PlcoutUp_temp(l_b,c_b); 
                                        plcout.Lo(l_b,c_b) = PlcoutLo_temp(l_b,c_b);
                                        yl.Up(l_b) = YlUp_temp(l_b); 
                                        yl.Lo(l_b) = YlLo_temp(l_b); 
                                        fsm.Up(sl,mj)$lout(l_b,sl) = FsmUp_temp(sl,mj); 
                                        fsm.Up(sl,ml)$(asm(sl,ml) and not ssm(sl,ml) and lout(l_b,sl)) = FsmUp_temp(sl,ml);
                                        fsm.Up(sl,ml)$(asm(sl,ml) and not ssm(sl,ml) and lin(ml,l_b)) = FsmUp_temp(sl,ml);
                                        #display "after restore";  
                                        #display plcout.Up, plcout.Lo, fsm.Up, yl.Lo;
                                        #display Interval;
                                        #display objval.L,WTN_gpp_RP_PN_Abt.objest;
                                        #不论是提前终止还是正常求解结束，判断可紧缩的方法一致,都可以基于当前objest
                                        if(WTN_gpp_RP_PN_Abt.objest > UB, Yplcout_pmr_Bt(l_b,c_b,npmr)$(ord(npmr)=Interval)=0; );#如果可紧缩记录下对应的区间值,继续执行紧缩                                         
                                        #display Yplcout_pmr_Bt;
                                        yplcout_pmr.Up(l_b,c_b,npmr)$(ord(npmr)=Interval) = 1;#还原
                                        yplcout_pmr.Lo(l_b,c_b,npmr)$(ord(npmr)=Interval) = 0;#还原
                                        #提前终止Abt方法:如果待紧缩变量最远侧子区间无法消减(对于靠近下边界，最远侧是子区间Npmr_U)，则跳出边界紧缩过程，优先判断
                                        break$(sum(npmr$(ord(npmr)=Interval and l_c_npmr(l_b,c_b,npmr) and ord(npmr)=Npmr_U(l_b,c_b)), Yplcout_pmr_Bt(l_b,c_b,npmr))=1) 3;
                                        #如果没实现子区间的紧缩,则跳出子区间紧缩
                                        break$(sum(npmr$(ord(npmr)=Interval and l_c_npmr(l_b,c_b,npmr)), Yplcout_pmr_Bt(l_b,c_b,npmr))=1); 
                                    );                                                                            
                                else
                                    #display "========================================";
                                    #display "Lb domain is tightened";
                                    for(Interval = 1 to (min(Npmr_USol(l_b,c_b),Npmr_LSol(l_b,c_b))-1) by 1,                                   
                                        yplcout_pmr.fx(l_b,c_b,npmr)$(ord(npmr)=Interval) = 1;

                                        #暂存边界值
                                        PlcoutUp_temp(l_b,c_b) = plcout.Up(l_b,c_b); 
                                        PlcoutLo_temp(l_b,c_b) = plcout.Lo(l_b,c_b);
                                        YlUp_temp(l_b) = yl.Up(l_b); 
                                        YlLo_temp(l_b) = yl.Lo(l_b); 
                                        FsmUp_temp(sl,mj)$(asm(sl,mj) and not ssm(sl,mj) and lout(l_b,sl)) = fsm.Up(sl,mj); 
                                        FsmUp_temp(sl,ml)$(asm(sl,ml) and not ssm(sl,ml) and lout(l_b,sl)) = fsm.Up(sl,ml);
                                        FsmUp_temp(sl,ml)$(asm(sl,ml) and not ssm(sl,ml) and lin(ml,l_b)) = fsm.Up(sl,ml);
                                        #display "before update";                                          
                                        #display plcout.Up, plcout.Lo, fsm.Up, yl.Lo;
                                        #增加对变量域的更新和更新相关其他变量的边界更新
                                        plcout.Up(l_b,c_b) = sum(npmr$(ord(npmr)=Interval), PlcoutnUp_pmr(l_b,c_b,npmr));
                                        plcout.Lo(l_b,c_b) = sum(npmr$(ord(npmr)=Interval), PlcoutnLo_pmr(l_b,c_b,npmr));
                                        #更新y
                                        if(plcout.Lo(l_b,c_b)>1E-3, yl.fx(l_b)=1);
                                        #更新flj,fll-lout,fll-min
                                        fsm.Up(sl,mj)$(asm(sl,mj) and not ssm(sl,mj) and lout(l_b,sl)) =
                                            (min(sum(i, Fi(i)), sum(j$jin(mj,j), FjU(j))))*sum(j$jin(mj,j),PjcU(j,c_b))/max(sum(j$jin(mj,j),PjcU(j,c_b)), plcout.Lo(l_b,c_b)); #flj_UP 
                                        fsm.Up(sl,ml)$(asm(sl,ml) and not ssm(sl,ml) and lout(l_b,sl)) =
                                            sum(i, Fi(i))*sum(la$lin(ml,la),plcout.Up(la,c_b))/max(sum(la$lin(ml,la),plcout.Up(la,c_b)), plcout.Lo(l_b,c_b)*sum(la$lin(ml,la),(1-Rlc(la,c_b)))); #fll_UP-lout                                         
                                        fsm.Up(sl,ml)$(asm(sl,ml) and not ssm(sl,ml) and lin(ml,l_b)) =
                                            sum(i, Fi(i))*plcout.Up(l_b,c_b)/max(plcout.Up(l_b,c_b), sum(la$lout(la,sl), plcout.Lo(la,c_b))*(1-Rlc(l_b,c_b))) ; #fll_UP-min                                                                              
                                        #display "after update";                                       
                                        #display plcout.Up, plcout.Lo, fsm.Up, yl.Lo;


                                        Solve WTN_gpp_RP_PN_Abt using MIP minimizing objval;
                                        Abt_Count = Abt_Count + 1;


                                        #还原变量边界避免影响其他问题(基于同样的变量)
                                        plcout.Up(l_b,c_b) = PlcoutUp_temp(l_b,c_b); 
                                        plcout.Lo(l_b,c_b) = PlcoutLo_temp(l_b,c_b);
                                        yl.Up(l_b) = YlUp_temp(l_b); 
                                        yl.Lo(l_b) = YlLo_temp(l_b); 
                                        fsm.Up(sl,mj)$lout(l_b,sl) = FsmUp_temp(sl,mj); 
                                        fsm.Up(sl,ml)$(asm(sl,ml) and not ssm(sl,ml) and lout(l_b,sl)) = FsmUp_temp(sl,ml);
                                        fsm.Up(sl,ml)$(asm(sl,ml) and not ssm(sl,ml) and lin(ml,l_b)) = FsmUp_temp(sl,ml);
                                        #display "after restore";
                                        #display plcout.Up, plcout.Lo, fsm.Up, yl.Lo;
                                        #display Interval;
                                        #display objval.L,WTN_gpp_RP_PN_Abt.objest;
                                        if(WTN_gpp_RP_PN_Abt.objest > UB, Yplcout_pmr_Bt(l_b,c_b,npmr)$(ord(npmr)=Interval)=0; );#如果可紧缩记录下对应的区间值,继续执行紧缩
                                        #display Yplcout_pmr_Bt;
                                        yplcout_pmr.Up(l_b,c_b,npmr)$(ord(npmr)=Interval) = 1;#还原
                                        yplcout_pmr.Lo(l_b,c_b,npmr)$(ord(npmr)=Interval) = 0;#还原
                                        #提前终止Abt方法:如果待紧缩变量最远侧子区间无法消减(对于靠近下边界，最远侧是子区间Npmr_U)，则跳出边界紧缩过程，优先判断
                                        break$(sum(npmr$(ord(npmr)=Interval and l_c_npmr(l_b,c_b,npmr) and ord(npmr)=1), Yplcout_pmr_Bt(l_b,c_b,npmr))=1) 3;
                                        #如果没实现子区间的紧缩,则跳出子区间紧缩
                                        break$(sum(npmr$(ord(npmr)=Interval and l_c_npmr(l_b,c_b,npmr)), Yplcout_pmr_Bt(l_b,c_b,npmr))=1); 
                                    );
                                );

                                # Step 4.2 ------ 如果实现了变量的边界紧缩（没有两次跳出发生）则更新分割方案和变量的边界以及辅助参数
                                #display "--------------before contraction---------------"
                                #display plcout.Up, plcout.Lo, fsm.Up, yl.Lo;
                                plcout.Up(l_b,c_b) = smax(npmr$Yplcout_pmr_Bt(l_b,c_b,npmr), PlcoutnUp_pmr(l_b,c_b,npmr));
                                plcout.Lo(l_b,c_b) = smin(npmr$Yplcout_pmr_Bt(l_b,c_b,npmr), PlcoutnLo_pmr(l_b,c_b,npmr));
                                #执行边界传播，增加对变量域(fsm)的更新和更新相关其他变量(yl)的边界更新
                                #更新y
                                if(plcout.Lo(l_b,c_b)>1E-3, yl.fx(l_b)=1);
                                #更新flj,fll-lout,fll-min
                                fsm.Up(sl,mj)$(asm(sl,mj) and not ssm(sl,mj) and lout(l_b,sl)) =
                                    (min(sum(i, Fi(i)), sum(j$jin(mj,j), FjU(j))))*sum(j$jin(mj,j),PjcU(j,c_b))/max(sum(j$jin(mj,j),PjcU(j,c_b)), plcout.Lo(l_b,c_b)); #flj_UP 
                                fsm.Up(sl,ml)$(asm(sl,ml) and not ssm(sl,ml) and lout(l_b,sl)) =
                                    sum(i, Fi(i))*sum(la$lin(ml,la),plcout.Up(la,c_b))/max(sum(la$lin(ml,la),plcout.Up(la,c_b)), plcout.Lo(l_b,c_b)*sum(la$lin(ml,la),(1-Rlc(la,c_b)))); #fll_UP-lout                                         
                                fsm.Up(sl,ml)$(asm(sl,ml) and not ssm(sl,ml) and lin(ml,l_b)) =
                                    sum(i, Fi(i))*plcout.Up(l_b,c_b)/max(plcout.Up(l_b,c_b), sum(la$lout(la,sl), plcout.Lo(la,c_b))*(1-Rlc(l_b,c_b))) ; #fll_UP-min                                                                              
                                #display "--------------after contraction---------------"
                                #display plcout.Up, plcout.Lo, fsm.Up, yl.Lo;


                                #先更新子区间边界，再更新分割点比较好
                                Count_bt = 0;
                                loop(npmr$(l_c_npmr(l_b,c_b,npmr) and Yplcout_pmr_Bt(l_b,c_b,npmr)),
                                    Count_bt = Count_bt +1;
                                    PlcoutnUp_pmr(l_b,c_b,npmr_a)$(ord(npmr_a)=Count_bt) = PlcoutnUp_pmr(l_b,c_b,npmr);
                                    PlcoutnLo_pmr(l_b,c_b,npmr_a)$(ord(npmr_a)=Count_bt) = PlcoutnLo_pmr(l_b,c_b,npmr);   
                                );                                    
                                #清零原来的子区间多余边界值#,原来的多余分割点值
                                PlcoutnUp_pmr(l_b,c_b,npmr)$(ord(npmr)>Count_bt and ord(npmr)<=Npmr_U(l_b,c_b)) = 0;
                                PlcoutnLo_pmr(l_b,c_b,npmr)$(ord(npmr)>Count_bt and ord(npmr)<=Npmr_U(l_b,c_b)) = 0;
                                Plcoutq_pmr(l_b,c_b,qpmr)$(ord(qpmr)>(Count_bt+1) and ord(qpmr)<=(Npmr_U(l_b,c_b)+1)) = 0;
                                #更新Npmr_U,l_c_npmr,l_c_qpmr,Plcoutq_pmr,Npmr_LSol,Npmr_USol
                                Npmr_U(l_b,c_b) = sum(npmr$l_c_npmr(l_b,c_b,npmr), Yplcout_pmr_Bt(l_b,c_b,npmr));
                                l_c_npmr(l_b,c_b,npmr) = yes$(ord(npmr) <= Npmr_U(l_b,c_b));
                                l_c_qpmr(l_b,c_b,qpmr) = yes$(ord(qpmr) <= (Npmr_U(l_b,c_b) + 1));
                                Plcoutq_pmr(l_b,c_b,qpmr)$(l_c_qpmr(l_b,c_b,qpmr) and ord(qpmr)<=Npmr_U(l_b,c_b)) =
                                    sum(npmr$(l_c_npmr(l_b,c_b,npmr) and ord(npmr)=ord(qpmr)),PlcoutnLo_pmr(l_b,c_b,npmr));
                                Plcoutq_pmr(l_b,c_b,qpmr)$(l_c_qpmr(l_b,c_b,qpmr) and ord(qpmr)=(Npmr_U(l_b,c_b)+1)) =
                                    sum(npmr$(l_c_npmr(l_b,c_b,npmr) and ord(npmr)=(ord(qpmr)-1)),PlcoutnUp_pmr(l_b,c_b,npmr));
                                Npmr_LSol(l_b,c_b) = sum(npmr$(PlcoutnUp_pmr(l_b,c_b,npmr) >= Plcout_LSol(l_b,c_b)
                                                               and PlcoutnLo_pmr(l_b,c_b,npmr) <= Plcout_LSol(l_b,c_b)
                                                               and l_c_npmr(l_b,c_b,npmr)), ord(npmr));
                                Npmr_USol(l_b,c_b) = sum(npmr$(PlcoutnUp_pmr(l_b,c_b,npmr) >= Plcout_USol(l_b,c_b)*1E-3
                                                               and PlcoutnLo_pmr(l_b,c_b,npmr) <= Plcout_USol(l_b,c_b)*1E-3
                                                               and l_c_npmr(l_b,c_b,npmr)), ord(npmr));
                                #display "---check---", Npmr_U, PlcoutnUp_pmr, PlcoutnLo_pmr, Plcoutq_pmr, Npmr_USol, Npmr_LSol, l_c_npmr, plcout.Up, plcout.Lo;
                                #-------------------------------
                                l_c_abt(l_b,c_b) = no;#此时的(l_b,c_b)是确定的
                                #display l_c_abt; 
                            );  
                        ); #一次loop只能执行一轮分割区间数最大的，需要多次执行，直到满足跳出条件
                        break$(card(l_c_abt) = 0);
                    );
                    Report_go(itergo, 'Abt_parti_redu') = sum((l,c),Npmr_U_temp(l,c))-sum((l,c),Npmr_U(l,c)); #只有执行紧缩的轮次才记录，跟Npmr_U_temp在同一个层次
                );
            );
            

            Report_go(itergo, 'Abt_time') = timeExec-Time_start_abt;
            Report_go(itergo, 'Abt_Count') = Abt_Count;
            Report_go(itergo, 'TotalTime234') = timeExec-Time_start_2;    
            #---------------------------------------------------------------------------------------
            #------------------------ GO-Step 3：Dynamic strengh the RP using PMR ------------------
            #---------------------------------------------------------------------------------------
            #Update dynamic partitions
            #更新分割区间数-对于每轮偏差值大于Dynamic_factor的都进行分割
            Npmr_U(l,c)$(Pclout_BLsum_devi(l,c) > Pclout_BLsum_devi_Ave*Dynamic_factor) = Npmr_U(l,c) + Nd_dynamic;
            #更新分割集合,分割点-基于动态均匀二分
            l_c_npmr(l,c,npmr) = yes$(ord(npmr) <= Npmr_U(l,c));
            l_c_qpmr(l,c,qpmr) = yes$(ord(qpmr) <= (Npmr_U(l,c) + 1));
            #method 1:增加Nd_dynamic个分割点，均匀分割子区间的通用写法+基于动态调节因子
            Plcoutq_pmr(l,c,qpmr)$(l_c_qpmr(l,c,qpmr) and (ord(qpmr) <= Npmr_LSol(l,c) ) and (Pclout_BLsum_devi(l,c) > Pclout_BLsum_devi_Ave*Dynamic_factor)) = Plcoutq_pmr(l,c,qpmr);
            Plcoutq_pmr(l,c,qpmr)$(l_c_qpmr(l,c,qpmr) and (ord(qpmr) > (Npmr_LSol(l,c) + Nd_dynamic)) and (Pclout_BLsum_devi(l,c) > Pclout_BLsum_devi_Ave*Dynamic_factor)) = Plcoutq_pmr(l,c,qpmr - Nd_dynamic);
            Plcoutq_pmr(l,c,qpmr)$(l_c_qpmr(l,c,qpmr) and ((ord(qpmr) > Npmr_LSol(l,c)) and (ord(qpmr) <= (Npmr_LSol(l,c) + Nd_dynamic))) and (Pclout_BLsum_devi(l,c) > Pclout_BLsum_devi_Ave*Dynamic_factor)) = 
                sum(qpmr_a$(ord(qpmr_a) = Npmr_LSol(l,c)), Plcoutq_pmr(l,c,qpmr_a))                                                                                                           
                +(sum(qpmr_a$(ord(qpmr_a) = Npmr_LSol(l,c) + Nd_dynamic + 1), Plcoutq_pmr(l,c,qpmr_a)) - sum(qpmr_a$(ord(qpmr_a) = Npmr_LSol(l,c)), Plcoutq_pmr(l,c,qpmr_a)))/(Nd_dynamic + 1)*(ord(qpmr) - Npmr_LSol(l,c));
            PlcoutnUp_pmr(l,c,npmr)$l_c_npmr(l,c,npmr) = sum(qpmr$(ord(qpmr) = (ord(npmr) + 1)), Plcoutq_pmr(l,c,qpmr));
            PlcoutnLo_pmr(l,c,npmr)$l_c_npmr(l,c,npmr) = sum(qpmr$(ord(qpmr) = ord(npmr)), Plcoutq_pmr(l,c,qpmr));
            #display l_c_npmr, Plcoutq_pmr;
            #严格的集合控制，不需要清零数据，不影响中间变量的正确更新
        );
    );

**============Result Report===========
    Report(case,'ObjVal') = UB;
    Report(case,'Total time') = timeExec-Time_start_1;
    Report(case,'NumEqu')$(1-Ygpp) = WTN.numEqu;
    Report(case,'NumVar')$(1-Ygpp) = WTN.numVar;
    Report(case,'NumDVar')$(1-Ygpp) = WTN.numDVar;
    Report(case,'NumEqu')$(Ygpp) = WTN_gpp.numEqu;
    Report(case,'NumVar')$(Ygpp) = WTN_gpp.numVar;
    Report(case,'NumDVar')$(Ygpp) = WTN_gpp.numDVar;
    Report(case,'NumBL') = card(l)*card(c)*(card(l)+card(j)-1+Yunitselfrecycled);
    Report(case,'NumL') = card(l);
    Report(case,'NumI') = card(i);
    Report(case,'NumL') = card(l);
    Report(case,'NumJ') = card(j);
    Report(case,'NumC') = card(c);#可以补充个节水率指标=优化后的新鲜水用量/总设备用水量
    #如果找到可行解，也输出
    if(Gap > Gap_Tol and Gap < 1,
        #display '============================================================================================================================'
        display 'the feasible soution is found!';
        display 'Feasible solutions:',UB, LB, Gap, Fl_USol, Fsm_USol, Plcin_USol, Plcout_USol, Pjc_USol, Fil_USol, Fij_USol, Fll_USol, Flj_USol;
        #display '============================================================================================================================='
        #display  Npmr_U;
        putclose dicopt_opt 'relaxed 1';#还原
        WTN_gpp.optfile = 0;#还原
        putclose bbcontrol 'mipstopexpr resusd >= 10000000';#还原
        WTN_gpp_RP_PN_Abt.optfile = 0;#还原
    );
    display Report_mul;
    display Report_go;
    #清理变量当前值和边界设定 避免影响下个算例的计算
    option
        clear = Report_mul
        clear = Report_go
        clear = sol
        clear = Objvalcou
        clear = Freq
        clear = fsm
        clear = plcout
        clear = yl
        clear = ysm
        clear = Fsm_USol
        clear = Fl_USol 
        clear = Plcout_USol  
        clear = Plcin_USol
        clear = Pjc_USol
        clear = Fil_USol
        clear = Fll_USol
        clear = Flj_USol
    ;
    display '                                                                 ';
);
display Report;



**--------Export data to Excel using Connect---------
*extract data to Excel workbook
*embeddedCode Connect:
*- GAMSReader:
*    symbols:
*      - name: Report
*- PandasExcelWriter:
*    file: 0627WUN_partition8_GNMDT_ENMDT.xlsx
*    symbols:
*      - name: Report
*        range: Report!A1
*endEmbeddedCode

